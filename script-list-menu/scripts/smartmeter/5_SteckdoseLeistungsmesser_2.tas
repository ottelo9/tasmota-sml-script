>D 100
;5_SteckdoseLeistungsmesser_2.tas
M:s4h=0 721
M:s4hf=0 6
M:s24h=0 1441
M:s24hf=0 12
M:dcon=0 31
M:mcon=0 12
p:mval=0
p:dval=0
p:yval=0
p:da=1
t:t1=5
t:t2=60
tmp=0
idx4=0
idx24=0
cstr="cnth0/720"
cstr2="cnth0/30"
utm="00d 00h 00m"
hr=0
fr=0
>B
sota("")
tmp=is(0 "Jan|Feb|Mär|Apr|Mai|Jun|Jul|Aug|Sep|Okt|Nov|Dez|")
smlj=0
=#load
#save
print saving arrays
dp2
fr=fo("data.csv" w)
if fr>=0 {
fwa(s4h fr)
fwa(s24h fr)
fwa(dcon fr)
fwa(mcon fr)
fc(fr)
} else {
print file error: %fr%
}
svars
#load
print loading arrays
dp2
fr=fo("data.csv" r)
fra(s4h fr)
fra(s24h fr)
fra(dcon fr)
fra(mcon fr)
fc(fr)
#init
dval=enrg[0]
mval=enrg[0]
yval=enrg[0]
acp(s4h 0)
acp(s24h 0)
->Backlog2 Timezone 99;TimeStd 0,0,10,1,3,60;TimeDst 0,0,3,1,2,120
=#save
#init2
acp(dcon 0)
acp(mcon 0)
=#save
#daysub
if wm>0 {
wcs <div id="day" style="text-align:center;width:600px;height:400px"></div>
wcs <script language="JavaScript">function drawChart(){
wcs var cssc={'headerRow':'hRow','rowNumberCell':'hCol','tableCell':'tCell'};
wcs var data=google.visualization.arrayToDataTable([['Tag','Energie [kWh]',{role: 'style'}],
for tmp 1 dcon[-1] 1
if (tmp==day) {
wcs [%tmp%,%dcon[tmp]%,'red'],
}
if (tmp<day) {
wcs [%tmp%,%dcon[tmp]%,'green'],
}
if (tmp>day) {
wcs [%tmp%,%dcon[tmp]%,''],
}
next
wcs ]);
wcs var options={chartArea:{left:40,right:30,height:'75%%'},legend:'none',title:'Tagesverbräuche (Monat %is[month]%)',vAxis:{format:'# kWh'},hAxis:{title:'Tag',ticks:[1,5,10,15,20,25,30]}};
wcs var chart=new google.visualization.ColumnChart(document.getElementById('day'));
wcs chart.draw(data,options);}google.charts.setOnLoadCallback(drawChart);</script>
}
#monthsub
if wm>0 {
wcs <div id="month" style="text-align:center;width:600px;height:400px"></div>
wcs <script language="JavaScript">function drawChart(){
wcs var cssc={'headerRow':'hRow','rowNumberCell':'hCol','tableCell':'tCell'};
wcs var data=google.visualization.arrayToDataTable([['Monat','Energie [kWh]',{role: 'style'}],
for tmp 1 12 1
if (tmp<month) {
wcs ['%is[tmp]%',%mcon[tmp]%,'green'],
}
if (tmp==month) {
wcs ['%is[tmp]%',%mcon[tmp]%,'red'],
}
if (tmp>month) {
wcs ['%is[tmp]%',%mcon[tmp]%,''],
}
next
wcs ]);
wcs var options={chartArea:{left:40,right:40,height:'75%%'},legend:'none',title:'Verbräuche (Jahr %0(year-1)%/%0year%)',vAxis:{format:'# kWh'}};
wcs var chart=new google.visualization.ColumnChart(document.getElementById('month'));
wcs chart.draw(data,options);}google.charts.setOnLoadCallback(drawChart);</script>
}
>S
if (year<2020) {
print auf NTP warten
break
}
if (enrg[1]==0) {
break
}
smlj=1
if (t1==0) {
t1=5
s4hf=enrg[7]
s24hf=enrg[7]
tmp=hours-1
if (tmp<0) {
tmp+=24
}
idx4=tmp*720+(mins*12)+int(secs/5)
idx24=(hours*60+mins)
s4h[0]=idx4%s4h[-1]
s4h=int(s4hf)
s24h[0]=idx24
s24h=int(s24hf)
cstr="cnth"+s(0idx4)+"/120"
cstr2="cnth"+s(0idx24)+"/60"
power2=(0.9*power2)+((1-0.9)*enrg[7])
=>publish stat/%topic%/script/power2 %power2%
}
if (t2==0) {
t2=60
hr=hours
utm=s(2.0(int(uptime/1440)))+"d "+s(2.0(int(uptime/60)%24))+"h "+s(2.0(uptime%60))+"m"
dcon[day]=enrg[0]-dval
mcon[month]=enrg[0]-mval
if ((chg[hr]>0) and (hr==0)) {
if (day>1) {
da=day
} else {
for tmp (da+1) 31 1
dcon[tmp]=0
next
mval=enrg[0]
}
if (day*month==1) {
yval=enrg[0]
}
dval=enrg[0]
=#save
}
}
>W
Leistung (gefiltert){m}%0power2% W
Tagesverbrauch{m}%2(enrg[0]-dval)% kWh
Monatsverbrauch{m}%2(enrg[0]-mval)% kWh
Jahresverbrauch{m}%2(enrg[0]-yval)% kWh
Datum{m}%s(2.0day)%.%s(2.0month)%.%s(2.0year)% - %s(2.0hours)%:%s(2.0mins)%:%s(2.0secs)%
Uptime{m}%utm%
$<div style="margin-left:-30px">
$<div id="chart1" style="text-align:center;width:600px;height:400px"></div>
$gc(lt s4h "wr" "Leistung [W]" cstr)
$var options = {
$chartArea:{left:60,right:20,height:'75%%'},
$legend:'none',
$vAxis:{format:'# W'},
$hAxis:{slantedTextAngle:45},
$explorer:{actions:['dragToZoom','rightClickToReset']},
$series: {0: {type: 'area'}},
$title:'Leistung 1 Stunde [Watt]'
$};
$gc(e)
$<div id="chart2" style="text-align:center;width:600px;height:400px"></div>
$gc(lt s24h "wr" "Leistung [W]" cstr2)
$var options = {
$chartArea:{left:60,right:20,height:'75%%'},
$legend:'none',
$vAxis:{format:'# W'},
$hAxis:{slantedTextAngle:45},
$explorer:{actions:['dragToZoom', 'rightClickToReset']},
$series: {0: {type: 'area'}},
$title:'Leistung 24 Stunden [Watt]'
$};
$gc(e)
%=#daysub
%=#monthsub
$<center><span style="font-size:10px;">
$Tasmota Script für Steckdosen mit Energiemessung (2)<br>
$Version 15.01.2026 by ottelo.jimdo.de & gemu2015 (ScriptEngine V%vers%)<br>
$Hinweis: Alle Werte werden um Mitternacht gespeichert!<br>
$4h Diagramm aktualisiert sich alle 5s, 24h Diagramm alle 60s.<br>
$</span></center></div>
>w Werte initialisieren / Daten sichern
<button onclick="history.back()">Zurück</button>
<hr>
<style>
.flex-container {
display: flex;
justify-content: center;
}
.center-flex-horizontally {
width: 270px;
background-color: #f0f0f0;
text-align: left;
padding: 20px;
border: 2px solid #ccc;
}
</style>
<div class="flex-container">
<div class="center-flex-horizontally">
<style>.button-group{display:flex;flex-direction:column;align-items:flex-start;gap:10px;}</style>
<div class=button-group>
<b>5_SteckdosenLeistungsmesser_2.tas</b>
<button type="submit" onclick="if(confirm('Tages/Monats/Jahreswerte und beide Diagramme (4/24h) zurücksetzen? Die Balkendiagramme bleiben erhalten.')){seva(1,'_init');}">Werte initialisieren</button>
<button type="submit" onclick="if(confirm('Balkendiagramme zurücksetzen?')){seva(1,'_init2');}">Reset Balkendiagramme</button>
<button type="button" onclick="seva(1,'_save');alert('Daten wurden gespeichert!');">Diagrammdaten speichern</button></div>
</div>
</div>
