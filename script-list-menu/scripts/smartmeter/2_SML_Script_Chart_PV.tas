>D 100
M:sday=0 1441
M:dcon=0 31
M:dprod=0 31
M:mcon=0 24
M:s4h=0 481
p:mval=0
p:dval=0
p:mval2=0
p:dval2=0
p:yval=0
p:yval2=0
p:da=1
p:rxind=4
p:txind=2
p:sel=0
t:t1=5
t:t2=30
t:t3=60
rxpin=3
txpin=1
tmp=0
idx4=0
idx24=0
cstr="cnt0/120"
cstr2="cnth0/60"
utm="00d 00h 00m"
avgv=0
avgv2=0
avgvc=0
avgvc2=0
hr=0
swesp=0
swespflg=0
power2=0
save=0
rst=0
fr=0
>B
rxpin=rxind-1
txpin=txind-1
->sensor53 r
tmp=is(0 "Jan|Feb|Mär|Apr|Mai|Jun|Jul|Aug|Sep|Okt|Nov|Dez|")
smlj=0
=#load
#save
print saving arrays
dp2
fr=fo("data.csv" w)
if fr>=0 {
fwa(s4h fr)
fwa(sday fr)
fwa(dcon fr)
fwa(dprod fr)
fwa(mcon fr)
fc(fr)
} else {
print file error: %fr%
}
svars
#load
print loading arrays
dp2
fr=fo("data.csv" r)
fra(s4h fr)
fra(sday fr)
fra(dcon fr)
fra(dprod fr)
fra(mcon fr)
fc(fr)
#init
dval=sml[2]
dval2=sml[3]
mval=sml[2]
mval2=sml[3]
yval=sml[2]
yval2=sml[3]
->Backlog2 otaurl KEIN OTA UPGRADE VIA WEBSERVER!
->Backlog2 Timezone 99;TimeStd 0,0,10,1,3,60;TimeDst 0,0,3,1,2,120
=#save
#daysub
if wm>0 {
wcs <div id="day" style="text-align:center;width:600px;height:400px"></div>
wcs <script language="JavaScript">function drawChart(){
wcs var cssc={'headerRow':'hRow','rowNumberCell':'hCol','tableCell':'tCell'};
wcs var data=google.visualization.arrayToDataTable([['Tag','Energie [kWh]',{role: 'style'}],
for tmp 1 dcon[-1] 1
if (tmp==day) {
wcs [%tmp%,%dcon[tmp]%,'red'],
}
if (tmp<day) {
wcs [%tmp%,%dcon[tmp]%,'green'],
}
if (tmp>day) {
wcs [%tmp%,%dcon[tmp]%,''],
}
next
wcs ]);
wcs var options={chartArea:{left:40,right:30,height:'75%%'},legend:'none',title:'Tagesverbräuche (Monat %is[month]%)',vAxis:{format:'# kWh'},hAxis:{title:'Tag',ticks:[1,5,10,15,20,25,30]}};
wcs var chart=new google.visualization.ColumnChart(document.getElementById('day'));
wcs chart.draw(data,options);}google.charts.setOnLoadCallback(drawChart);</script>
}
#dayprod
if wm>0 {
wcs <div id="dayp" style="text-align:center;width:600px;height:400px"></div>
wcs <script language="JavaScript">function drawChart(){
wcs var cssc={'headerRow':'hRow','rowNumberCell':'hCol','tableCell':'tCell'};
wcs var data=google.visualization.arrayToDataTable([['Tag','Energie [kWh]',{role: 'style'}],
for tmp 1 dprod[-1] 1
if (tmp==day) {
wcs [%tmp%,%dprod[tmp]%,'red'],
}
if (tmp<day) {
wcs [%tmp%,%dprod[tmp]%,'green'],
}
if (tmp>day) {
wcs [%tmp%,%dprod[tmp]%,''],
}
next
wcs ]);
wcs var options={chartArea:{left:40,right:30,height:'75%%'},legend:'none',title:'Tageseinspeisung (Monat %is[month]%)',vAxis:{format:'# kWh'},hAxis:{title:'Tag',ticks:[1,5,10,15,20,25,30]}};
wcs var chart=new google.visualization.ColumnChart(document.getElementById('dayp'));
wcs chart.draw(data,options);}google.charts.setOnLoadCallback(drawChart);</script>
}
#monthsub
if wm>0 {
wcs <div id="month" style="text-align:center;width:600px;height:400px"></div>
wcs <script language="JavaScript">function drawChart(){
wcs var cssc={'headerRow':'hRow','rowNumberCell':'hCol','tableCell':'tCell'};
wcs var data=google.visualization.arrayToDataTable([['Monat','Verbrauch [kWh]','Einspeisung[kWh]'],
for tmp 1 12 1
if (tmp<month) {
wcs ['%is[tmp]%',%mcon[tmp]%,%mcon[tmp+12]%],
}
if (tmp==month) {
wcs ['%is[tmp]%',%mcon[tmp]%,%mcon[tmp+12]%],
}
if (tmp>month) {
wcs ['%is[tmp]%',%mcon[tmp]%,%mcon[tmp+12]%],
}
next
wcs ]);
wcs var options={series:{0:{targetAxisIndex:0},1:{targetAxisIndex:1}},chartArea:{left:40,right:40,height:'75%%'},legend:'none',title:'Verbräuche / Einspeisungen (Jahr %0(year-1)%/%0year%)',vAxes:{0:{format:'# kWh'},1:{format:'# kWh'}}};
wcs var chart=new google.visualization.ColumnChart(document.getElementById('month'));
wcs chart.draw(data,options);}google.charts.setOnLoadCallback(drawChart);</script>
}
>C
if eres==1 {
->sensor53 r
svars
eres=0
}
>F
tmp=fe("/sml_ftask.tas")
>S
tmp=fe("/sml_stask.tas")
if (save==1) {
save=0
=#save
}
if (rst==1) {
rst=0
=#init
}
if (year<2020) {
print auf NTP warten
break
}
smlj=1
idx4=int((((hours+20)*120)+(mins*2)+(int(secs/30)))%2880+1)
idx24=int(hours*60+mins+1)
s4h[0]=idx4/5
sday[0]=idx24
cstr="cnth"+s(0idx4)+"/120"
cstr2="cnth"+s(0idx24)+"/60"
if ((chg[rxind]>0) or (chg[txind]>0)) {
rxpin=rxind-1
->sensor53 r
=#save
}
if (sml[2]==0) {
break
}
if (t1==0) {
t1=5
avgv+=sml[1]
avgvc+=1
avgv2+=sml[1]
avgvc2+=1
power2=(0.9*power2)+((1-0.9)*sml[1])
if (swesp==1) {
if ((power2<-300) and (swespflg==0)) {
swespflg=1
}
if ((power2>100) and (swespflg==1)) {
swespflg=0
}
}
}
if (t2==0) {
t2=30
s4h[idx4/5]=int(avgv/avgvc)
avgv=0
avgvc=0
}
if (t3==0) {
t3=60
hr=hours
utm=s(2.0(int(uptime/1440)))+"d "+s(2.0(int(uptime/60)%24))+"h "+s(2.0(uptime%60))+"m"
dcon[day]=sml[2]-dval
mcon[month]=sml[2]-mval
dprod[day]=sml[3]-dval2
mcon[month+12]=sml[3]-mval2
sday=int(avgv2/avgvc2)
avgv2=0
avgvc2=0
if ((chg[hr]>0) and (hr==0)) {
if (day>1) {
da=day
} else {
for tmp (da+1) 31 1
dcon[tmp]=0
dprod[tmp]=0
next
mval=sml[2]
mval2=sml[3]
}
if (day*month==1) {
yval=sml[2]
yval2=sml[3]
}
dval=sml[2]
dval2=sml[3]
=#save
}
}
>W
bu(save "gespeichert!" "Daten sofort speichern")
Leistung (gefiltert){m}%0power2% W
Tagesverbrauch{m}%2(sml[2]-dval)% kWh
Monatsverbrauch{m}%2(sml[2]-mval)% kWh
Jahresverbrauch{m}%2(sml[2]-yval)% kWh
Tageseinspeisung{m}%2(sml[3]-dval2)% kWh
Monatseinspeisung{m}%2(sml[3]-mval2)% kWh
Jahreseinspeisung{m}%2(sml[3]-yval2)% kWh
Datum{m}%s(2.0day)%.%s(2.0month)%.%s(2.0year)% - %s(2.0hours)%:%s(2.0mins)%:%s(2.0secs)%
Uptime{m}%utm%
$<div style="margin-left:-30px">
$<div id="chart1" style="text-align:center;width:600px;height:400px"></div>
$gc(lt s4h "wr" "Leistung [W]" cstr)
$var options = {
$chartArea:{left:60,right:20,height:'75%%'},
$legend:'none',
$vAxis:{format:'# W'},
$hAxis:{slantedTextAngle:45},
$explorer:{actions:['dragToZoom','rightClickToReset']},
$series: {0: {type: 'area'}},
$title:'Leistung 4 Stunden [Watt]'
$};
$gc(e)
$<div id="chart2" style="text-align:center;width:600px;height:400px"></div>
$gc(lt sday "wr" "Leistung [W]" cstr2)
$var options = {
$chartArea:{left:60,right:20,height:'75%%'},
$legend:'none',
$vAxis:{format:'# W'},
$hAxis:{slantedTextAngle:45},
$explorer:{actions:['dragToZoom', 'rightClickToReset']},
$series: {0: {type: 'area'}},
$title:'Leistung 24 Stunden [Watt]'
$};
$gc(e)
%=#daysub
%=#dayprod
%=#monthsub
$<center><span style="font-size:10px;">
$Version 17.11.2025 (PV) by ottelo.jimdo.de<br>
$Credits to gemu2015 (Tasmota Script Dev)<br>
$Hinweis: Die Daten (ohne 4h Chart) werden um Mitternacht gespeichert!<br>
$Der Restart Button speichert ebenfalls die Daten.<br>
$</span></center></div>
>w Stromzählerauswahl
$<button onclick="history.back()">Zurück</button>
$<hr>
$<div><button type="submit" onclick="if(confirm('Werte initialisieren?')){seva(1,'rst');}">Tages/Monats/Jahreswerte initialisieren</button></div>
so(17)
smlpd("https://raw.githubusercontent.com/ottelo9/tasmota-sml-script/main/script-list-menu/meters" "Stromzählerauswahl" sel)
pd(rxind "RX Pin: " 50 "#g")
pd(txind "TX Pin: " 50 "#g")
