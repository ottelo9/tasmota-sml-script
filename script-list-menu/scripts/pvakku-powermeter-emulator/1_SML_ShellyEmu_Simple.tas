>D 250
;1_SML_ShellyEmu_Simple.tas
p:dval=0
p:dval2=0
p:mval2=0
p:mval=0
p:yval=0
p:yval2=0
p:rxind=1
p:txind=1
p:sel=0
p:pwroffset=0
p:pwrforce=0
p:throttle=1
p:port=1
t:t1=60
rxpin=0
txpin=0
hr=0
res=0
tmp=0
c1p=0
c2p=0
c3p=0
c1c=0
c2c=0
c3c=0
cpwr=0
str=""
mstr1=""
mstr2=""
mstr3=""
header=""
once=0
>B
->SetOption57 0
sota("")
rxpin=rxind-1
txpin=txind-1
->sensor53 r
smlj=0
throttle=mp(throttle <1 1 >60 60)
pwroffset=mp(pwroffset <-200 -200 >200 200)
#save
svars
#init
dval=sml[2]
dval2=sml[3]
mval=sml[2]
mval2=sml[3]
yval=sml[2]
yval2=sml[3]
->Backlog2 Timezone 99;TimeStd 0,0,10,1,3,60;TimeDst 0,0,3,1,2,120
=#save
>ah
res=won(1 "/rpc/*")
>on1
str=warg
if (str!="") {
res=ins(str "EM.GetStatus")
if res>=0 {
=#getstat
tmp=sl(mstr1)+sl(mstr2)
=#header
wcs so(4)
wcs %header%
wcs %mstr1%
wcs %mstr2%
wcf
break
}
res=ins(str "Shelly.GetStatus")
if res>=0 {
=#getshellystat
tmp=sl(mstr1)+sl(mstr2)+sl(mstr3)
=#header
wcs so(4)
wcs %header%
wcs %mstr1%
wcs %mstr2%
wcs %mstr3%
wcf
break
}
print unknown rpc request: %str%
}
#getstat
dp(0 2)
mstr1="{\"id\":0,\"a_act_power\":"+s(c1p)+",\"b_act_power\":"+s(c2p)+","
mstr2="\"c_act_power\":"+s(c3p)+",\"total_act_power\":"+s(cpwr)+"}}"
#getshellystat
dp(0 2)
mstr1="{\"em:0\":{\"id\":0,\"a_act_power\":"+s(c1p)+",\"b_act_power\":"+s(c2p)+",\"c_act_power\":"+s(c3p)+",\"total_act_power\":"+s(cpwr)+"},"
mstr2="\"emdata:0\":{\"id\":0,\"a_total_act_energy\":"+s(sml[2])+",\"a_total_act_ret_energy\":"+s(sml[3])+",\"b_total_act_energy\":0,\"b_total_act_ret_energy\":0,\"c_total_act_energy\":0,\"c_total_act_ret_energy\":0,"
mstr3="\"total_act\":"+s(sml[2])+",\"total_act_ret\":"+s(sml[3])+"}}"
#header
header="HTTP/1.1 200 OK\r\nServer: ShellyHTTP/1.0.0\r\nContent-Type: application/json\r\nContent-Length: "+s(0tmp)+"\r\nConnection: close\r\n\r\n"
>C
if eres==1 {
->sensor53 r
svars
eres=0
}
>F
fe("sml_ftask.tas")
str=udp(1)
if (str!="") {
header="{\"id\":0,\"src\":\"shellypro3em-"+maca+"\",\"result\":"
res=ins(str "EM.GetStatus")
if (res>=0) {
=#getstat
udp(2 header mstr1 mstr2)
break
}
res=ins(str "Shelly.GetStatus")
if res>=0 {
=#getshellystat
mstr3+="}"
udp(2 header mstr1 mstr2)
udp(2 mstr3)
break
}
}
>S
fe("sml_stask.tas")
if ((chg[rxind]>0) or (chg[txind]>0)) {
rxpin=rxind-1
->sensor53 r
=#save
}
if ((year<2020) or (sml[2]==0)) {
print auf NTP/Zähler warten
break
}
smlj=1
if (t1==0) {
t1=60
hr=hours
if ((chg[hr]>0) and (hr==0)) {
dval=sml[2]
dval2=sml[3]
if (day==1) {
mval=sml[2]
mval2=sml[3]
}
if (day*month==1) {
yval=sml[2]
yval2=sml[3]
}
=#save
}
}
if (once==0) {
mdns("shellypro3em-" "-" "shelly")
if (port==2) {
udp(0 2220)
} else {
udp(0 1010)
}
once=1
}
if (upsecs%throttle==0) {
tmp=sml[1]-cpwr
if (tmp>500) {
cpwr=sml[1]-(tmp/2)
} else {
cpwr=sml[1]-pwroffset
}
}
c1p=cpwr
c1c=c1p/230
if (pwrforce>0) {
=#getstat
udp(2 header mstr1 mstr2)
}
>W
Leistung (an Marstek Akku){m}%0cpwr% W
Tagesverbrauch{m}%2(sml[2]-dval)% kWh
Monatsverbrauch{m}%2(sml[2]-mval)% kWh
Jahresverbrauch{m}%2(sml[2]-yval)% kWh
Tageseinspeisung{m}%2(sml[3]-dval2)% kWh
Monatseinspeisung{m}%2(sml[3]-mval2)% kWh
Jahreseinspeisung{m}%2(sml[3]-yval2)% kWh
Uptime{m}%0(int(uptime/1440))% d %0(int((uptime%1440)/60))% h %0(uptime%60)% min
$<center><span style="font-size:10px;">
$Version 21.02.2026 (ShellyEmuSimple) by ottelo.jimdo.de<br>
$und gemu2015 (ScriptEngine V%vers%)<br>
$Hinweis: Alle Werte werden um Mitternacht gespeichert!<br>
$</span></center>
>w &#x2699;&#xFE0F; Stromzähler / Daten / Akku
<button onclick="history.back()">&#x2B05;&#xFE0F; Zurück</button>
<hr>
<style>
.flex-container{
display:flex;
justify-content:center;
}
.center-flex-horizontally{
width:270px;
background-color:#f0f0f0;
text-align:left;
padding:20px;
border:2px solid #ccc;
}
</style>
so(17+4)
<div class="flex-container">
<div class="center-flex-horizontally">
<style>.button-group{display:flex;flex-direction:column;align-items:flex-start;gap:10px;}</style>
<div class=button-group>
<b>&#x1F4C4; 1_SML_ShellyEmu_Simple</b>
<button type="submit" onclick="if(confirm('Tages/Monats/Jahreswerte zurücksetzen?')){seva(1,'_init');}">&#x1F504; Zählerwerte initialisieren</button>
<button type="button" onclick="seva(1,'_save');alert('Daten wurden gespeichert!');">&#x1F4BE; Daten speichern</button></div>
<hr>
<b>&#x1F50B; Akku</b><br><br>
Offset für Nulleinspeisung [W]:
nm(-200 200 1 pwroffset "" 50 0)
Update der Werte verzögern [s]:
nm(1 60 1 throttle "" 50 0)
UDP-Port (Neustart notwendig):
rb(port "" "1010" "2220")
Datenübertragung erzwingen:
ck(pwrforce "")
<hr>
<b>&#x26A1; Stromzähler</b><br>
smlpd("https://raw.githubusercontent.com/ottelo9/tasmota-sml-script/main/script-list-menu/meters" "Stromzählerauswahl (<a href=https://ottelo.jimdofree.com/kontakt>Probleme?)</a><br>" sel)
pd(rxind "RX Pin: " 50 "#gr")
pd(txind "TX Pin: " 50 "#gr")
<span style="font-size:9px">
RX=3 TX=1: Hichi, Stromleser, LesekopfV32, Wattwächter<br>
RX=5 TX=4: bitShake<br></span>
<br>
&#x1F4DD; <a href="/ufse?file=/sml_meter.def">Stromzähler Script</a>
<hr>
<b>&#x2139;&#xFE0F; System</b><br>
Script %0slen%/16384 chars | FS %0fsi(1)%/%0fsi(0)% KB
</div>
</div>
