>D 250
;1_SML_EcoTrackerEmu_Simple.tas
p:dval=0
p:dval2=0
p:mval2=0
p:mval=0
p:yval=0
p:yval2=0
p:rxind=1
p:txind=1
p:sel=0
p:pwroffset=0
t:t1=60
rxpin=0
txpin=0
hr=0
res=0
tmp=0
cpwr=0
once=0
header=""
json=""
>B
sota("")
rxpin=rxind-1
txpin=txind-1
->sensor53 r
smlj=0
#save
svars
#init
dval=sml[2]
dval2=sml[3]
mval=sml[2]
mval2=sml[3]
yval=sml[2]
yval2=sml[3]
->Backlog2 Timezone 99;TimeStd 0,0,10,1,3,60;TimeDst 0,0,3,1,2,120
=#save
>ah
res=won(1 "/v1/json")
>on1
json="{\"power\":"+s(0cpwr)+",\"powerAvg\":"+s(0cpwr)+",\"energyCounterIn\":"+s(0sml[2]*1000)+",\"energyCounterOut\":"+s(0sml[3]*1000)+"}"
tmp=sl(json)
header="HTTP/1.1 200 OK\r\nContent-Length: "+s(0tmp)+"\r\nContent-Type: application/json\r\n\r\n"
wcs so(4)
wcs %header%
wcs %json%
wcf
>C
if eres==1 {
->sensor53 r
svars
eres=0
}
>F
fe("sml_ftask.tas")
>S
fe("sml_stask.tas")
if ((chg[rxind]>0) or (chg[txind]>0)) {
rxpin=rxind-1
->sensor53 r
=#save
}
if ((year<2020) or (sml[2]==0)) {
print auf NTP/Zähler warten
break
}
smlj=1
if (t1==0) {
t1=60
hr=hours
if ((chg[hr]>0) and (hr==0)) {
dval=sml[2]
dval2=sml[3]
if (day==1) {
mval=sml[2]
mval2=sml[3]
}
if (day*month==1) {
yval=sml[2]
yval2=sml[3]
}
=#save
}
}
if (once==0) {
res=mdns("ecotracker-" "-" "everhome")
once=1
}
tmp=sml[1]-cpwr
if (tmp>500) {
cpwr=sml[1]-(tmp/2)
} else {
cpwr=sml[1]-pwroffset
}
>W
Leistung (an Marstek Akku){m}%0cpwr% W
Tagesverbrauch{m}%2(sml[2]-dval)% kWh
Monatsverbrauch{m}%2(sml[2]-mval)% kWh
Jahresverbrauch{m}%2(sml[2]-yval)% kWh
Tageseinspeisung{m}%2(sml[3]-dval2)% kWh
Monatseinspeisung{m}%2(sml[3]-mval2)% kWh
Jahreseinspeisung{m}%2(sml[3]-yval2)% kWh
Uptime{m}%0(int(uptime/1440))%d %0(int((uptime%1440)/60))%h %0(uptime%60)%m
$<center><span style="font-size:10px;">
$Version 15.01.2026 (EcoTrackerEmuSimple) by ottelo.jimdo.de<br>
$und gemu2015 (ScriptEngine V%vers%)<br>
$Hinweis: Alle Werte werden um Mitternacht gespeichert!<br>
$</span></center>
>w Stromzähler konfigurieren / Daten sichern
<button onclick="history.back()">Zurück</button>
<hr>
<style>
.flex-container {
display: flex;
justify-content: center;
}
.center-flex-horizontally {
width: 270px;
background-color: #f0f0f0;
text-align: left;
padding: 20px;
border: 2px solid #ccc;
}
</style>
so(17+4)
<div class="flex-container">
<div class="center-flex-horizontally">
<style>.button-group{display:flex;flex-direction:column;align-items:flex-start;gap:10px;}</style>
<div class=button-group>
<b>1_SML_EcoTrackerEmu_Simple.tas</b>
<button type="submit" onclick="if(confirm('Tages/Monats/Jahreswerte zurücksetzen?')){seva(1,'_init');}">Zählerwerte initialisieren</button>
<button type="button" onclick="seva(1,'_save');alert('Daten wurden gespeichert!');">Daten speichern</button></div>
<hr>
Offset für Nulleinspeisung [W]:
nm(-100 100 1 pwroffset "" 50 0)
<hr>
smlpd("https://raw.githubusercontent.com/ottelo9/tasmota-sml-script/main/script-list-menu/meters" "Stromzählerauswahl (<a href=https://ottelo.jimdofree.com/kontakt>Probleme?)</a><br>" sel)
pd(rxind "RX Pin: " 50 "#gr")
pd(txind "TX Pin: " 50 "#gr")
<span style="font-size:9px">
RX=3 TX=1: Hichi, Stromleser, LesekopfV32, Wattwächter<br>
RX=5 TX=4: bitShake<br></span>
<br>
<a href="/ufse?file=/sml_meter.def">=> Stromzähler Script</a>
</div>
</div>
