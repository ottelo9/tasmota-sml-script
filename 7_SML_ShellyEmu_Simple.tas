>D 250
; Einfaches Shelly/EcoTracker Emulator Script für ESP8266/ESP32 und Tasmota Image von ottelo
; Emuliert Shelly Pro 3EM oder EcoTracker für Marstek Akku Jupiter, Venus, B2500,Growatt Noah 2000(aktuell ungetestet) ...
; Anleitung https://ottelo.jimdofree.com/stromz%C3%A4hler-auslesen-tasmota/#13a
; Script basiert auf https://github.com/gemu2015/Sonoff-Tasmota/blob/universal/tasmota/scripting/shelly_emu_script.tas
; Script ist für ein Wifi Tasmota ESP Lesekopf für Stromzähler gedacht, z.B. HichiV1/V2, Bitshake, usw.
; Der Text kann einfach so in den internen Editor von Tasmota eingefügt werden. Beim ESP8266 sollten alle Kommentare entfernen werden.
; 
res=0
c1p=0
c2p=0
c3p=0
c1c=0
c2c=0
c3c=0
cpwr=0
kwhin=0
kwhout=0
str=""
tstr=""
mstr1=""
mstr2=""
mstr3=""
mstr4=""
mstr5=""
header=""
once=0
throttle=1
tmp=0
Current_Time=""
I:unixts=1548028800


>B
=>sensor53 r

; if you modify this section you must restart tasmota
>ah
; Shelly
res=won(1 "/rpc/*")
res=won(2 "/status")
; Ecotracker
res=won(3 "/v1/json")

>on1
;print Shelly rpc request
str=warg

res=ins(str "EM.GetStatus")
if res>=0 {
	wcs so(4)
	=#htph
	wcs %mstr1%
	wcs %header%
	=#getemstat
	wcs %mstr1%
	wcs %mstr2%
	wcf
	break
}

res=ins(str "Shelly.GetDeviceInfo")
if res>=0 {
	wcs so(4)
	=#htph
	wcs %mstr1%	
	=#getdefi	
	wcs %header%
	wcs %mstr1%
	wcs %mstr2%
	wcf	
	break
}

res=ins(str "EM.GetConfig")
if res>=0 {
	wcs so(4)
	=#htph
	wcs %mstr1%	
	=#getcfg
	wcs %header%
	wcs %mstr1%
	wcf	
	break
}

res=ins(str "EMData.GetStatus")
if res>=0 {
	wcs so(4)
	=#htph
	wcs %mstr1%
	=#egetstat
	wcs %header%
	wcs %mstr1%
	wcf
	break
}
res=ins(str "Shelly.GetStatus")
if res>=0 {
      wcs so(4)
      =#htph
      wcs %mstr1%
      =#getshellystat
      wcs %mstr1%
      wcs %mstr2%
      wcs %mstr3%
      wcs %mstr4%
      wcs %mstr5%
      wcf
      break
}
print unknown http equest: %str%

>on2
;print Shelly status request
wcs so(4)
=#htph
wcs %mstr1%
dp(0 2)
wcs {"Power": %cpwr%,"E_in":%kwhin%,"E_out":%kwhout%}
wcf

>on3
;print Ecotracker
wcs so(4)
=#htph
wcs %mstr1%
dp(0 2)
wcs {"energyCounterIn":%kwhin%,"energyCounterOut":%kwhout%,"powerAvg":%cpwr%,"energyCounterInT1":0,
wcs "energyCounterInT2":0,"power":%cpwr%}
wcf

#htph
mstr1="HTTP/1.1 200 OK\r\nContent-type: application/json\r\n\r\n"

#getcfg
mstr1="{\"id\":0,\"name\":null,\"blink_mode_selector\":\"active_energy\",\"phase_selector\":\"a\",\"monitor_phase_sequence\":true,\"ct_type\":\"120A\"}}"

#getdefi
mstr1="{\"name\":\""+tstr+"\",\"id\":\""+tstr+"\",\"mac\":\""+maca+"\",\"slot\":1,\"model\":\"SPEM-003CEBEU\","
mstr2="\"gen\":2,\"fw_id\":\"20241011-114455/1.4.4-g6d2a586\","
mstr2+="\"ver\":\"1.4.4\",\"app\":\"Pro3EM\",\"auth_en\":0,\"profile\":\"triphase\"}}"

#getemstat
dp(0 2)
mstr1="{\"id\":0,\"a_current\":"+s(c1c)+",\"a_voltage\":230,\"a_act_power\":"+s(c1p)+",\"a_aprt_power\":"+s(c1p)+",\"a_pf\":1,\"a_freq\":50,"
mstr1+="\"b_current\":"+s(c2c)+",\"b_voltage\":230,\"b_act_power\":"+s(c2p)+",\"b_aprt_power\":"+s(c2p)+",\"b_pf\":1,\"b_freq\":50,"
mstr2="\"c_current\":"+s(c3c)+",\"c_voltage\":230,\"c_act_power\":"+s(c3p)+",\"c_aprt_power\":"+s(c3p)+",\"c_pf\":1,\"c_freq\":50,"
mstr2+="\"total_current\":"+s(c1c+c2c+c3c)+",\"total_act_power\":"+s(cpwr)+",\"total_aprt_power\":"+s(cpwr)+"}}"

#egetstat
dp(0 2)
mstr1="{\"id\":0,\"a_total_act_energy\":"+s(c1p)+",\"a_total_act_ret_energy\":"+s(c1p)+",\"b_total_act_energy\":"+s(c2p)+",\"b_total_act_ret_energy\":"+s(c2p)+",\"c_total_act_energy\":"+s(c3p)+",\"c_total_act_ret_energy\":"+s(c3p)+",\"total_act\":"+s(cpwr)+",\"total_act_ret\":"+s(cpwr)+"}}"

#getshellystat
; Currently raw meter data is used, so no peak power detection etc.  
;Currently, this Function is for single-phase use.
; For 3-phase meters, values for phase B and C should also be set.  
; With multiple phases, some totals (e.g. total_current, total_act_power, total_apparent_power, total_power) may need to be provided depending on the system.  
Current_Time= s(2.0hours)
Current_Time+=":"
Current_Time+= s(2.0mins)
unixts=i(1548028800)
unixts+=i(epoch)
mstr1 = "{\"wifi_sta\":{\"connected\":true,\"ssid\":\"kabellos\",\"ip\":\""+lip+"\",\"rssi\":-40},";
mstr1 += "\"cloud\":{\"enabled\":false,\"connected\":false},";
mstr1 += "\"mqtt\":{\"connected\":false},";
mstr1 += "\"mac\":\""+maca+"\",\"has_update\":false";
;mstr1- alles außer die MAC und die  IP ist Statisch
mstr2 = ",\"unixtime\":"+s(unixts)+","
mstr2 += "\"time\":\""+Current_Time+"\","
;mstr2 = ",\"time\":\"09:37\",\"unixtime\":1756021020,"
mstr2 += "\"ram_total\":50648,\"ram_free\":38376,";
mstr2 += "\"ram_lwm\":32968,\"fs_size\":233681,\"fs_free\":174194,";
mstr2 += "\"temperature\":28.08,\"overtemperature\":false,\"tmp\":{\"tC\":28.08,\"tF\":82.54,\"is_valid\":true},\"fs_mounted\":true,"


mstr3 = "\"total_power\":"+s(sml[3])+",";
mstr3 += "\"em:0\":{";
mstr3 += "\"id\":0,";
mstr3 += "\"a_current\":"+s(sml[5])+",\"a_voltage\":"+s(sml[4])+",\"a_act_power\":"+s(sml[3])+",";
mstr3 += "\"a_aprt_power\":"+s(sml[3])+",\"a_pf\":1,\"a_freq\":"+s(sml[6])+",";
mstr3 += "\"b_current\":0,\"b_voltage\":"+s(sml[4])+",\"b_act_power\":0,";
mstr3 += "\"b_aprt_power\":0,\"b_pf\":1,\"b_freq\":0,";
mstr4 = "\"c_current\":0,\"c_voltage\":"+s(sml[4])+",\"c_act_power\":0,";
mstr4 += "\"c_aprt_power\":0,\"c_pf\":1,\"c_freq\":0,";
mstr4 += "\"total_current\":"+s(sml[5])+",\"total_act_power\":"+s(sml[3])+",\"total_aprt_power\":"+s(sml[3])+"},";
mstr4 += "\"emdata:0\":{";
mstr4 += "\"id\":0,";
mstr4 += "\"a_total_act_energy\":"+s(sml[1])+",\"a_total_act_ret_energy\":"+s(sml[2])+",";
mstr5 = "\"b_total_act_energy\":0,\"b_total_act_ret_energy\":0,";
mstr5 += "\"c_total_act_energy\":0,\"c_total_act_ret_energy\":0,";
mstr5 += "\"total_act\":"+s(sml[1])+",\"total_act_ret\":"+s(sml[2])+"},";
mstr5 += "\"modbus\":{\"enabled\":false},\"serial\":1,\"uptime\":"+s(upsecs)+"}"

>S
if year<2000 {
	break
}

if once==0 {
	; Option A: start mdns and udp rpc handler to emulate Shelly
	res=mdns("shellypro3em-" "-" "shelly")
	; port 1010 or port 2220 (for b2500)
	res=udp(0 1010)
	tstr="shellypro3em-"+maca
	; Option B: start mdns to emulate EcoTracker from everHome
	;res=mdns("ecotracker-" "-" "everhome")
	;tstr="ecotracker-"+maca
	;-----------------------
	; build header string
	header="{\"id\":0,\"src\":\""+tstr+"\",\"result\":"
	once=1
}

; only update every throttle seconds 1=every sec
if upsecs%throttle==0 {

; current power, reduce power peaks (e.g. refrigerator)
tmp=sml[1]-cpwr
if (tmp>500) {
	cpwr=sml[1]-(tmp/2)
} else {
	cpwr=sml[1]
}
; energy in, energy out
kwhin=sml[2]
kwhout=sml[3]

; default: meter send only power sum value
c1p=cpwr/3

; optional: use this (remove ;) if your meter send 3 phase power values. Marstek then checks which phase it is connected to.
; recommended not to use this
;c1p=sml[4]
;c2p=sml[5]
;c3p=sml[6]
; calculate phase currents
c1c=c1p/230
;c2c=c2p/230
;c3c=c3p/230

}


; evaluate udp input
str=udp(1)
if str!="" {
	;print udp rpc payload=%str%
	res=ins(str "EM.GetStatus")
	if res>=0 {
		=#getstat
		udp(2 header mstr1 mstr2)
		break
	}
	res=ins(str "Shelly.GetDeviceInfo")
	if res>=0 {
		=#getdefi
		udp(2 header mstr1 mstr2)
		break
	}
	res=ins(str "EM.GetConfig")
	if res>=0 {
		=#getcfg
		udp(2 header mstr1)
		break
	}
	res=ins(str "EMData.GetStatus")
	if res>=0 {
		=#egetstat
		udp(2 header mstr1)
		break
	}
} else {
	; if no request, send the package anyway. This reduces oscillation!!
	=#getstat
	udp(2 header mstr1 mstr2)
}


>W
Uptime{m}%uptime% min
Power to Marstek{m}%cpwr% W
$<center><span style="font-size:10px;">
$Version 25.08.2025 (ShellyEmuSimple) by ottelo.jimdo.de<br>
$Shelly Pro 3EM Emulation by gemu2015/Kalle<br>
$</span></center>


; adapt your own meter descriptor here
; dont change order! 1=Gesamtleistung sml[1] 2=Verbrauch sml[2] 3=Einspeisung sml[3]
>M 1
; Dont use the median filter (<flag> = 16) ! Otherwise Marstek will oscillate.
; https://tasmota.github.io/docs/Smart-Meter-Interface/#meter-definition
+1,5,o,0,9600,eBZ,4
1,1-0:16.7.0*255(@1,Gesamtleistung,W,Power,0
1,1-0:1.8.0*255(@1,Verbrauch,kWh,E_in,3
1,1-0:2.8.0*255(@1,Einspeisung,kWh,E_out,3
; optional: 3 Phase meter values sml[4], sml[5], sml[6]
;1,1-0:36.7.0*255(@1,Leistung L1,W,36_7_0,0
;1,1-0:56.7.0*255(@1,Leistung L2,W,56_7_0,0
;1,1-0:76.7.0*255(@1,Leistung L3,W,76_7_0,0
#
