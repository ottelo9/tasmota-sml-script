>D 100
; IP for external ScriptEditor
IP=192.168.178.132

; ACHTUNG: KRITISCHE ÄNDERUNG AB SCRIPT VERSION 01.12.2025
; DIAGRAMM DATEN MÜSSEN VOR DEM UPGRADE AUF DIESE VERSION GESICHERT WERDEN!
; NACH DEM UPDATE KÖNNEN DIE DATEN WIEDER IMPORTIERT WERDEN
; ANLEITUNG: https://ottelo.jimdofree.com/stromz%C3%A4hler-auslesen-tasmota/#14
; Die Daten werden nun in einer extra Datei "data.csv" gespeichert und können so jederzeit gesichert werden!
; DIESES SCRIPT IST NUR NOCH FÜR DEN ESP32
; TASMOTA (ottelo) IMAGE ab v15.1.0 (07.12.2025) NOTWENDIG


; (1) Beschreibung:
; Skript Version ganz unten
; Tasmota Script für smarte Steckdosen / Energiemesssteckdosen (z.B. Sonoff POW Elite, SONOFF POW320D, NOUS A8T)
; Script nicht mehr für ESP8266/8685 (Sonoff Pow R2, Nous A1T), nutzt dazu das Script https://github.com/ottelo9/tasmota-sml-script/blob/main/ESP8266/5_SonoffPowR2_GosundEP2_NousA1T.tas
; Diese Variante 2 bietet im Gegensatz zum anderen Script (1) ein sehr fein aufgelöstes Diagramm für die Momentanleistung der letzten Stunde (alle 5s ein Wert)
; Tasmota Skript mit Google Chart (Diagramme)
; 2 Liniendiagramme: Leistung der letzten Stunde (fein aufgelöst, 5s) und Leistung der letzten 24h (grob aufgelöst, 60s)
; Tabellen mit Anzeige des Netzbezugs bzw. Netzeinspeisung (bei PV) pro Tag und pro Monat
; Tasmota muss mit dem Internet verbunden sein um die aktuelle Uhrzeit via NTP zu erhalten. Ansonsten muss das Skript angepasst werden.
; Optional im Skript enthalten: (Op1) Daten an www.clever-pv.com senden. Zum Aktivieren nach (OpX) suchen und inkl ; entfernen.

; (2) Anleitung:
; http://ottelo.jimdo.de/
; (a) Script übertragen
; Dieses Script kann ab Tasmota 15.1.0-ottelo bequem via DropDown Menü unter Tools > Edit Script ausgewählt werden.
; Anschließend könnt ihr über den Button "Werte initialisieren / Daten sichern" die Werte initialisieren.
; Ansonsten müsst ihr es vor dem Einfügen in Tasmota verkleinern/komprimieren. Anleitung siehe Link oben.


; -- ARRAYS (max:10, MAXFILT) --
; 1h Diagramm, neuer Wert alle 5s, 720+1=Optimierung X-Achse
M:s4h=0 721
M:s4hf=0 6
; 24h Diagramm, neuer Wert alle 60s, 1440+1=Optimierung X-Achse
M:s24h=0 1441
M:s24hf=0 12
; Tagesnetzbezug Tabelle 1-31
M:dcon=0 31
; Monatsverbrauch Tabelle 1-12
M:mcon=0 12
; -- VARS --
; PermVars (max:1536bytes=384vars)
p:mval=0
p:dval=0
p:yval=0
p:da=1
;p:vn=12000   optional um per Slider Y-Achse Werte zu begrenzen
t:t1=5
t:t2=60
tmp=0
idx4=0
idx24=0
cstr="cnth0/720"
cstr2="cnth0/30"
utm="00d 00h 00m"
hr=0
fr=0


; -- BOOT --
>B
sota("")
tmp=is(0 "Jan|Feb|Mär|Apr|Mai|Jun|Jul|Aug|Sep|Okt|Nov|Dez|")
;Deaktiviere MQTT beim Start, verhindert das falsche Werte gesendet werden
smlj=0
=#load


; -- SUBS --
#save
print saving arrays
dp2
fr=fo("data.csv" w)
if fr>=0 {
fwa(s4h fr)
fwa(s24h fr)
fwa(dcon fr)
fwa(mcon fr)
fc(fr)
} else {
print file error: %fr%
}
svars

#load
print loading arrays
dp2
fr=fo("data.csv" r)
fra(s4h fr)
fra(s24h fr)
fra(dcon fr)
fra(mcon fr)
fc(fr)

#init
dval=enrg[0]
mval=enrg[0]
yval=enrg[0]
acp(s4h 0)
acp(s24h 0)
->Backlog2 Timezone 99;TimeStd 0,0,10,1,3,60;TimeDst 0,0,3,1,2,120
=#save

#init2
acp(dcon 0)
acp(mcon 0)
=#save

; Tagesverbrauch Tabelle
#daysub
if wm>0 {
wcs <div id="day" style="text-align:center;width:600px;height:400px"></div>
wcs <script language="JavaScript">function drawChart(){
wcs var cssc={'headerRow':'hRow','rowNumberCell':'hCol','tableCell':'tCell'};
wcs var data=google.visualization.arrayToDataTable([['Tag','Energie [kWh]',{role: 'style'}],
for tmp 1 dcon[-1] 1
	if (tmp==day) {
		wcs [%tmp%,%dcon[tmp]%,'red'],
	}
	if (tmp<day) {
		wcs [%tmp%,%dcon[tmp]%,'green'],
	}
	if (tmp>day) {
		wcs [%tmp%,%dcon[tmp]%,''],
	}
next
wcs ]);
wcs var options={chartArea:{left:40,right:30,height:'75%%'},legend:'none',title:'Tagesverbräuche (Monat %is[month]%)',vAxis:{format:'# kWh'},hAxis:{title:'Tag',ticks:[1,5,10,15,20,25,30]}};
wcs var chart=new google.visualization.ColumnChart(document.getElementById('day'));
wcs chart.draw(data,options);}google.charts.setOnLoadCallback(drawChart);</script>
}

; Netzverbrauch Monat Tabelle
#monthsub
if wm>0 {
wcs <div id="month" style="text-align:center;width:600px;height:400px"></div>
wcs <script language="JavaScript">function drawChart(){
wcs var cssc={'headerRow':'hRow','rowNumberCell':'hCol','tableCell':'tCell'};
wcs var data=google.visualization.arrayToDataTable([['Monat','Energie [kWh]',{role: 'style'}],
for tmp 1 12 1
	if (tmp<month) {
		wcs ['%is[tmp]%',%mcon[tmp]%,'green'],
	}
	if (tmp==month) {
		wcs ['%is[tmp]%',%mcon[tmp]%,'red'],
	}
	if (tmp>month) {
		wcs ['%is[tmp]%',%mcon[tmp]%,''],
	}
next
wcs ]);
wcs var options={chartArea:{left:40,right:40,height:'75%%'},legend:'none',title:'Verbräuche (Jahr %0(year-1)%/%0year%)',vAxis:{format:'# kWh'}};
wcs var chart=new google.visualization.ColumnChart(document.getElementById('month'));
wcs chart.draw(data,options);}google.charts.setOnLoadCallback(drawChart);</script>
}


; -- JEDE SEKUNDE --
>S
; Warte auf NTP
if (year<2020) {
	print auf NTP warten
	break
}
; Warte auf Steckdose
if (enrg[1]==0) {
	break
}
; Starte MQTT erst wenn Daten von der Steckdose kommen
smlj=1

; alle 5s
if (t1==0) {
	t1=5
	; enrg[n] = hole Wert vom energy register n 0=Gesamtverbrauch, 1..3 Spannung von Phase 1..3, 4..6 aktuell von Phase 1..3, 7..9 Leistung von Phase 1..3, 10=Verbrauch ab Start, 11=Tagesverbrauch, 12=Verbrauch Gestern (USE_ENERGY_SENSOR)
	; Moving average filter für 4h/24h Diagramm
	s4hf=enrg[7]
	s24hf=enrg[7]
	
	; 4h/24h Array idx setzen
	; Wie wird idx4 (bzw. cstr) für 4h X-Achse errechnet
	; setze 1h Chart X-Achse alle 5s => /720 Werte pro Stunde = Array
	; (aktuelle Stunde-1) * (720 Werte pro Stunde) + (aktuelle Minute * 12 Werte pro Minute) + (aktuelle Sekunde * 1 Wert /5)
	tmp=hours-1
	if (tmp<0) {
		tmp+=24
	}
	idx4=tmp*720+(mins*12)+int(secs/5)
	idx24=(hours*60+mins)
	; 4h Chart. Durschnittsleistung [W] der letzten 30s => s4h[1-480]
	s4h[0]=idx4%s4h[-1]
	s4h=int(s4hf)
	; 24h Chart. Durschnittsleistung [W] der letzten 60s => s24h[1-1440]
	s24h[0]=idx24
	s24h=int(s24hf)
	cstr="cnth"+s(0idx4)+"/120"
	cstr2="cnth"+s(0idx24)+"/60"
	
	; Optional: stark gemittelte Leistung für z.B. opendtu-onbattery DPL: http://192.168.178.31/cm?user=admin&password=joke&cmnd=script?power2
	power2=(0.9*power2)+((1-0.9)*enrg[7])
	=>publish stat/%topic%/script/power2 %power2%

	; (Op1) Siehe https://ottelo.jimdofree.com/stromz%C3%A4hler-auslesen-tasmota/#8
	;->WebQuery EURE-PUSH-API-URL POST [Content-Type:application/json] {"watt": %0enrg[7]%}
}

; alle 60s
if (t2==0) {
	t2=60
	hr=hours
	;Zeit seit letztem Boot
	utm=s(2.0(int(uptime/1440)))+"d "+s(2.0(int(uptime/60)%24))+"h "+s(2.0(uptime%60))+"m"

	; Tagesverbrauch [kWh]
	dcon[day]=enrg[0]-dval
	; Monatsverbrauch [kWh]
	mcon[month]=enrg[0]-mval
	
	;=>publish stat/%topic%/script/consday %2(dcon[day])%
	;=>publish stat/%topic%/script/consmonth %2(mcon[month])%
	;=>publish stat/%topic%/script/consyear %2(enrg[0]-yval)%

	; Tagesverbrauch Berechnung um Mitternacht
	if ((chg[hr]>0) and (hr==0)) {
		if (day>1) {
			da=day
		} else {
		    ; Monatswechsel
			for tmp (da+1) 31 1
				dcon[tmp]=0
			next
			; monthly values
			mval=enrg[0]
		}
		if (day*month==1) {
			; Jahreswechsel
			yval=enrg[0]
		}
		; daily values
		dval=enrg[0]
		; nur 1x um Mitternacht speichern
		=#save
	}
}


; WEB INTERFACE
>W
; Auto reload
;$<script> setTimeout("location.reload(true);",5000); </script>

;Tasmota Buttons ausblenden. Um an die Menüs zu kommen z.B. http://tasmota-ip/mn? eingeben
;<style>form button{display:none}</style>

; web button
; optional um per Slider Y-Achse Werte zu begrenzen, suche im Skript nach vn
;sl(4000 20000 vn "4 kW" "12 kW" "20 kW")

; Filtered power
Leistung (gefiltert){m}%0power2% W

; Verbrauch
Tagesverbrauch{m}%2(enrg[0]-dval)% kWh
Monatsverbrauch{m}%2(enrg[0]-mval)% kWh
Jahresverbrauch{m}%2(enrg[0]-yval)% kWh

; Zeit
Datum{m}%s(2.0day)%.%s(2.0month)%.%s(2.0year)% - %s(2.0hours)%:%s(2.0mins)%:%s(2.0secs)%
Uptime{m}%utm%
$<div style="margin-left:-30px">

; 1h hochaufgelöstes Leistung Diagramm (5s Raster)
$<div id="chart1" style="text-align:center;width:600px;height:400px"></div>
$gc(lt s4h "wr" "Leistung [W]" cstr)
$var options = {
$chartArea:{left:60,right:20,height:'75%%'},
$legend:'none',
;$vAxis:{format:'# W',viewWindow:{max:%vn%}},   optional um per Slider Y-Achse Werte zu begrenzen, suche im Skript nach vn
;$vAxis:{format:'# W',viewWindow:{max:5000}},   optional um Y-Achse Werte auf 5kW zu begrenzen
$vAxis:{format:'# W'},
$hAxis:{slantedTextAngle:45},
$explorer:{actions:['dragToZoom','rightClickToReset']},
$series: {0: {type: 'area'}},
$title:'Leistung 1 Stunde [Watt]'
$};
$gc(e)

; 24h Leistung Diagramm
$<div id="chart2" style="text-align:center;width:600px;height:400px"></div>
$gc(lt s24h "wr" "Leistung [W]" cstr2)
$var options = {
$chartArea:{left:60,right:20,height:'75%%'},
$legend:'none',
;$vAxis:{format:'# W',viewWindow:{max:%vn%}},   optional um per Slider Y-Achse Werte zu begrenzen, suche im Skript nach vn
;$vAxis:{format:'# W',viewWindow:{max:5000}},   optional um Y-Achse Werte auf 5kW zu begrenzen
$vAxis:{format:'# W'},
$hAxis:{slantedTextAngle:45},
$explorer:{actions:['dragToZoom', 'rightClickToReset']},
$series: {0: {type: 'area'}},
$title:'Leistung 24 Stunden [Watt]'
$};
$gc(e)

; Einfacher Säulenchart mit 2 Reihen
;$<div id="chart3" style="text-align:center;width:600px;height:400px"></div>
;$gc(c dprod "wr" "kWh" "cnt1" "Tägliche Einspeisung")

; Tagesverbrauch Tabelle
%=#daysub

; Netzverbrauch Monat Tabelle
%=#monthsub

$<center><span style="font-size:10px;">
$Tasmota Script für Steckdosen mit Energiemessung (2)
$Version 15.01.2026 by ottelo.jimdo.de & gemu2015 (ScriptEngine V%vers%)<br>
$Hinweis: Alle Werte werden um Mitternacht gespeichert!<br>
$4h Diagramm aktualisiert sich alle 5s, 24h Diagramm alle 60s.<br>
$</span></center></div>


>w Werte initialisieren / Daten sichern
<button onclick="history.back()">Zurück</button>
<hr>
<style>
.flex-container {
display: flex;
justify-content: center;
}
.center-flex-horizontally {
width: 270px;
background-color: #f0f0f0;
text-align: left;
padding: 20px;
border: 2px solid #ccc;
}
</style>
<div class="flex-container">
<div class="center-flex-horizontally">  
<style>.button-group{display:flex;flex-direction:column;align-items:flex-start;gap:10px;}</style>
<div class=button-group>
<b>5_SteckdosenLeistungsmesser_2.tas</b>
<button type="submit" onclick="if(confirm('Tages/Monats/Jahreswerte und beide Diagramme (4/24h) zurücksetzen? Die Balkendiagramme bleiben erhalten.')){seva(1,'_init');}">Werte initialisieren</button>
<button type="submit" onclick="if(confirm('Balkendiagramme zurücksetzen?')){seva(1,'_init2');}">Reset Balkendiagramme</button>
<button type="button" onclick="seva(1,'_save');alert('Daten wurden gespeichert!');">Diagrammdaten speichern</button></div>
</div>
</div>
