>D 100
; IP for external ScriptEditor
IP=192.168.178.132

; ACHTUNG: KRITISCHE √ÑNDERUNG AB SCRIPT VERSION 01.12.2025
; DIAGRAMM DATEN M√úSSEN VOR DEM UPGRADE AUF DIESE VERSION GESICHERT WERDEN!
; NACH DEM UPDATE K√ñNNEN DIE DATEN WIEDER IMPORTIERT WERDEN
; ANLEITUNG: https://ottelo.jimdofree.com/stromz%C3%A4hler-auslesen-tasmota/#14
; Die Daten werden nun in einer extra Datei "data.csv" gespeichert und k√∂nnen so jederzeit gesichert werden!
; DIESES SCRIPT IST NUR NOCH F√úR DEN ESP32
; TASMOTA (ottelo) IMAGE ab v15.1.0 (07.12.2025) NOTWENDIG

; MODIFIKATION: Historie in history.csv
; Speichert je nach Einstellung: St√ºndlich/T√§glich/Monatlich/J√§hrlich
; Detaillevel w√§hlbar √ºber Dropdown im Konfigurationsmen√º
; 1=Aus, 2=J√§hrlich, 3=Monatlich, 4=T√§glich, 5=St√ºndlich

; (1) Beschreibung:
; Skript Version ganz unten
; Tasmota SML Skript mit Google Chart (Diagramme)
; Diese Variante (2) bietet im Gegensatz zum anderen Script ein sehr hoch aufgel√∂stes Diagramm f√ºr die Momentanleistung der letzten Stunde (alle 5s ein Wert)
; 2 Liniendiagramme: Leistung der letzten Stunde (sehr fein aufgel√∂st, 5s) und Leistung der letzten 24h (grob aufgel√∂st, 60s)
; Tabellen mit Anzeige des Netzbezugs bzw. Netzeinspeisung (bei PV) pro Tag und pro Monat
; Tasmota muss mit dem Internet verbunden sein um die aktuelle Uhrzeit via NTP zu erhalten. Ansonsten muss das Skript angepasst werden.
; Optional im Skript enthalten: (Op1) Schalte zweiten ESP (z.B. Poolpumpe) bei Netzeinspeisung (Op2) Daten an www.clever-pv.com senden. Zum Aktivieren nach (OpX) suchen und inkl ; entfernen.

; (2) Anleitung:
; http://ottelo.jimdo.de/
; (a) Script √ºbertragen
; Dieses Script kann ab Tasmota 15.1.0-ottelo bequem via DropDown Men√º unter Tools > Edit Script ausgew√§hlt werden.
; Anschlie√üend k√∂nnt ihr √ºber den Button "Stromz√§hler konfigurieren / Daten sichern" euren Stromz√§hler w√§hlen und die Werte initialisieren.
; Ansonsten m√ºsst ihr es vor dem Einf√ºgen in Tasmota verkleinern/komprimieren. Anleitung siehe Link oben.


; -- ARRAYS (max:10, MAXFILT) --
; 1h Diagramm, neuer Wert alle 5s, 720+1=Optimierung X-Achse
M:s4h=0 721
M:s4hf=0 6
; 24h Diagramm, neuer Wert alle 60s, 1440+1=Optimierung X-Achse
M:s24h=0 1441
M:s24hf=0 12
; Tagesnetzbezug Tabelle 1-31
M:dcon=0 31
; Tagesnetzeinspeisung Tabelle 1-31
M:dprod=0 31
; Monatsverbrauch Tabelle 1-12, Einspeisung 13-24
M:mcon=0 24
; -- VARS --
; PermVars (max:1536bytes=384vars)
p:mval=0
p:dval=0
p:mval2=0
p:dval2=0
p:yval=0
p:yval2=0
p:da=1
p:rxind=1
p:txind=1
p:sel=0
; History Detail Level (1=Aus, 2=J√§hrlich, 3=Monatlich, 4=T√§glich, 5=St√ºndlich)
p:hlvl=1
; Letzter gespeicherter Tag/Monat/Jahr (um Doppelspeicherung zu vermeiden)
p:lhr=-1
p:lday=0
p:lmon=0
p:lyear=0
;p:vn=12000   optional um per Slider Y-Achse Werte zu begrenzen
t:t1=5
t:t2=60
rxpin=0
txpin=0
tmp=0
idx4=0
idx24=0
cstr="cnth0/720"
cstr2="cnth0/30"
hr=0
swesp=0
swespflg=0
power2=0
fr=0
res=0


; -- BOOT --
>B
sota("")
rxpin=rxind-1
txpin=txind-1
->sensor53 r
tmp=is(0 "Jan|Feb|M√§r|Apr|Mai|Jun|Jul|Aug|Sep|Okt|Nov|Dez|")
;Deaktiviere MQTT beim Start, verhindert das falsche Werte gesendet werden
smlj=0
=#load


; -- SUBS --
#save
print saving arrays
dp2
fr=fo("data.csv" w)
if fr>=0 {
fwa(s4h fr)
fwa(s24h fr)
fwa(dcon fr)
fwa(dprod fr)
fwa(mcon fr)
fc(fr)
} else {
print file error: %fr%
}
svars

#load
print loading arrays
dp2
fr=fo("data.csv" r)
fra(s4h fr)
fra(s24h fr)
fra(dcon fr)
fra(dprod fr)
fra(mcon fr)
fc(fr)

#init
dval=sml[2]
dval2=sml[3]
mval=sml[2]
mval2=sml[3]
yval=sml[2]
yval2=sml[3]
acp(s4h 0)
acp(s24h 0)
->Backlog2 Timezone 99;TimeStd 0,0,10,1,3,60;TimeDst 0,0,3,1,2,120
=#save

#init2
acp(dcon 0)
acp(dprod 0)
acp(mcon 0)
=#save

; History Eintrag schreiben
#loghist
fr=fo("history.csv" a)
if fr>=0 {
	; Format: Stunde;Tag;Monat;Jahr;Z√§hlerstand_Bezug_kWh;Z√§hlerstand_Einspeisung_kWh;Leistung_W
	res=fw(s(0hours)+";"+s(0day)+";"+s(0month)+";"+s(0year)+";"+s(2sml[2])+";"+s(2sml[3])+";"+s(0sml[1])+"\n" fr)
	fc(fr)
	print History: %0hours%h %0day%.%0month%.%0year% (Level %0hlvl%)
}

; Tagesverbrauch Tabelle
#daysub
if wm>0 {
wcs <div id="day" style="text-align:center;width:600px;height:400px"></div>
wcs <script language="JavaScript">function drawChart(){
wcs var cssc={'headerRow':'hRow','rowNumberCell':'hCol','tableCell':'tCell'};
wcs var data=google.visualization.arrayToDataTable([['Tag','Energie [kWh]',{role: 'style'}],
for tmp 1 dcon[-1] 1
	if (tmp==day) {
		wcs [%tmp%,%dcon[tmp]%,'red'],
	}
	if (tmp<day) {
		wcs [%tmp%,%dcon[tmp]%,'green'],
	}
	if (tmp>day) {
		wcs [%tmp%,%dcon[tmp]%,''],
	}
next
wcs ]);
wcs var options={chartArea:{left:40,right:30,height:'75%%'},legend:'none',title:'Tagesverbr√§uche (Monat %is[month]%)',vAxis:{format:'# kWh'},hAxis:{title:'Tag',ticks:[1,5,10,15,20,25,30]}};
wcs var chart=new google.visualization.ColumnChart(document.getElementById('day'));
wcs chart.draw(data,options);}google.charts.setOnLoadCallback(drawChart);</script>
}

; Tageseinspeisung PV Tabelle
#dayprod
if wm>0 {
wcs <div id="dayp" style="text-align:center;width:600px;height:400px"></div>
wcs <script language="JavaScript">function drawChart(){
wcs var cssc={'headerRow':'hRow','rowNumberCell':'hCol','tableCell':'tCell'};
wcs var data=google.visualization.arrayToDataTable([['Tag','Energie [kWh]',{role: 'style'}],
for tmp 1 dprod[-1] 1
	if (tmp==day) {
		wcs [%tmp%,%dprod[tmp]%,'red'],
	}
	if (tmp<day) {
		wcs [%tmp%,%dprod[tmp]%,'green'],
	}
	if (tmp>day) {
		wcs [%tmp%,%dprod[tmp]%,''],
	}
next
wcs ]);
wcs var options={chartArea:{left:40,right:30,height:'75%%'},legend:'none',title:'Tageseinspeisung (Monat %is[month]%)',vAxis:{format:'# kWh'},hAxis:{title:'Tag',ticks:[1,5,10,15,20,25,30]}};
wcs var chart=new google.visualization.ColumnChart(document.getElementById('dayp'));
wcs chart.draw(data,options);}google.charts.setOnLoadCallback(drawChart);</script>
}

; Netzverbrauch und Einspeisung Monat Tabelle
#monthsub
if wm>0 {
wcs <div id="month" style="text-align:center;width:600px;height:400px"></div>
wcs <script language="JavaScript">function drawChart(){
wcs var cssc={'headerRow':'hRow','rowNumberCell':'hCol','tableCell':'tCell'};
wcs var data=google.visualization.arrayToDataTable([['Monat','Verbrauch [kWh]','Einspeisung[kWh]'],
for tmp 1 12 1
	if (tmp<month) {
		wcs ['%is[tmp]%',%mcon[tmp]%,%mcon[tmp+12]%],
	}
	if (tmp==month) {
		wcs ['%is[tmp]%',%mcon[tmp]%,%mcon[tmp+12]%],
	}
	if (tmp>month) {
		wcs ['%is[tmp]%',%mcon[tmp]%,%mcon[tmp+12]%],
	}
next
wcs ]);
wcs var options={series:{0:{targetAxisIndex:0},1:{targetAxisIndex:1}},chartArea:{left:40,right:40,height:'75%%'},legend:'none',title:'Verbr√§uche / Einspeisungen (Jahr %0(year-1)%/%0year%)',vAxes:{0:{format:'# kWh'},1:{format:'# kWh'}}};
wcs var chart=new google.visualization.ColumnChart(document.getElementById('month'));
wcs chart.draw(data,options);}google.charts.setOnLoadCallback(drawChart);</script>
}


; -- USE_HTML_CALLBACK --
>C
if eres==1 {
	->sensor53 r
	svars
	eres=0
}


; -- JEDE 100MS --
>F
fe("sml_ftask.tas")


; -- JEDE SEKUNDE --
>S
fe("sml_stask.tas")
; Warte auf NTP
if (year<2020) {
	print auf NTP warten
	break
}
; Pinauswahl
if ((chg[rxind]>0) or (chg[txind]>0)) {
	rxpin=rxind-1
	->sensor53 r
	=#save
}
; History Level ge√§ndert -> speichern
if (chg[hlvl]>1) {
	svars
	print History Level: %0hlvl%
}
; Warte auf Z√§hler
if (sml[2]==0) {
	break
}
; Starte MQTT erst wenn Daten vom Stromz√§hler kommen
smlj=1

; alle 5s
if (t1==0) {
	t1=5
	; Moving average filter f√ºr 4h/24h Diagramm
	s4hf=sml[1]
	s24hf=sml[1]
	
	; 4h/24h Array idx setzen
	; Wie wird idx4 (bzw. cstr) f√ºr 4h X-Achse errechnet
	; setze 1h Chart X-Achse alle 5s => /720 Werte pro Stunde = Array
	; (aktuelle Stunde-1) * (720 Werte pro Stunde) + (aktuelle Minute * 12 Werte pro Minute) + (aktuelle Sekunde * 1 Wert /5)
	tmp=hours-1
	if (tmp<0) {
		tmp+=24
	}
	idx4=tmp*720+(mins*12)+int(secs/5)
	idx24=(hours*60+mins)
	; 4h Chart. Durschnittsleistung [W] der letzten 30s => s4h[1-480]
	s4h[0]=idx4%s4h[-1]
	s4h=int(s4hf)
	; 24h Chart. Durschnittsleistung [W] der letzten 60s => s24h[1-1440]
	s24h[0]=idx24
	s24h=int(s24hf)
	cstr="cnth"+s(0idx4)+"/120"
	cstr2="cnth"+s(0idx24)+"/60"
	
	; Optional: stark gemittelte Leistung f√ºr z.B. opendtu-onbattery DPL: http://192.168.178.31/cm?user=admin&password=joke&cmnd=script?power2
	power2=(0.9*power2)+((1-0.9)*sml[1])
	=>publish stat/%topic%/script/power2 %power2%

	; (Op1) Schalte anderen ESP bei Netzeinspeisung. Schaue in die >W Sektion.
	if (swesp==1) {
		if ((power2<-300) and (swespflg==0)) {
			;->websend [192.168.0.50] power ON
			;print power ON %power2%W
			swespflg=1
		}
		if ((power2>100) and (swespflg==1)) {
			;->websend [192.168.0.50] power OFF
			;print power OFF %power2%W
			swespflg=0
		}
	}
	; (Op2) Siehe https://ottelo.jimdofree.com/stromz%C3%A4hler-auslesen-tasmota/#8
	;->WebQuery EURE-PUSH-API-URL POST [Content-Type:application/json] {"watt": %0sml[1]%}
}

; alle 60s
if (t2==0) {
	t2=60
	hr=hours

	; History speichern je nach Level
	; 1=Aus, 2=J√§hrlich, 3=Monatlich, 4=T√§glich, 5=St√ºndlich
	
	; Level 5: St√ºndlich (bei jedem Stundenwechsel)
	if ((hlvl==5) and (hours!=lhr)) {
		lhr=hours
		=#loghist
	}
	
	; Level 4: T√§glich (um Mitternacht)
	if ((hlvl==4) and (hours==0) and (day!=lday)) {
		lday=day
		=#loghist
	}
	
	; Level 3: Monatlich (am 1. um Mitternacht)
	if ((hlvl==3) and (day==1) and (hours==0) and (month!=lmon)) {
		lmon=month
		=#loghist
	}
	
	; Level 2: J√§hrlich (am 1.1. um Mitternacht)
	if ((hlvl==2) and (day==1) and (month==1) and (hours==0) and (year!=lyear)) {
		lyear=year
		=#loghist
	}

	; Tagesverbrauch [kWh]
	dcon[day]=sml[2]-dval
	; Monatsverbrauch [kWh]
	mcon[month]=sml[2]-mval
	; Tageseinspeisung [kWh]
	dprod[day]=sml[3]-dval2
	; Monatseinspeisung [kWh]
	mcon[month+12]=sml[3]-mval2
	
	;=>publish stat/%topic%/script/consday %2(dcon[day])%
	;=>publish stat/%topic%/script/yieldday %2(dprod[day])%
	;=>publish stat/%topic%/script/consmonth %2(mcon[month])%
	;=>publish stat/%topic%/script/yieldmonth %2(mcon[month+12])%
	;=>publish stat/%topic%/script/consyear %2(sml[2]-yval)%
	;=>publish stat/%topic%/script/yielyear %2(sml[3]-yval2)%

	; Tagesverbrauch & Einspeisung Berechnung um Mitternacht
	if ((chg[hr]>0) and (hr==0)) {
		if (day>1) {
			da=day
		} else {
		    ; Monatswechsel
			for tmp (da+1) 31 1
				dcon[tmp]=0
				dprod[tmp]=0
			next
			; monthly values
			mval=sml[2]
			mval2=sml[3]
		}
		if (day*month==1) {
			; Jahreswechsel
			yval=sml[2]
			yval2=sml[3]
		}
		; daily values
		dval=sml[2]
		dval2=sml[3]
		; nur 1x um Mitternacht speichern
		=#save
	}
}


; WEB INTERFACE
>W
; Auto reload
;$<script> setTimeout("location.reload(true);",5000); </script>

;Tasmota Buttons ausblenden. Um an die Men√ºs zu kommen z.B. http://tasmota-ip/mn? eingeben
;<style>form button{display:none}</style>

; web button
; (Op1) Schalte anderen ESP bei Netzeinspeisung. Schaue in die >S Sektion.
;bu(swesp "PV Steckdose aktivieren" "PV Steckdose deaktivieren")
; optional um per Slider Y-Achse Werte zu begrenzen, suche im Skript nach vn
;sl(4000 20000 vn "4 kW" "12 kW" "20 kW")

; Filtered power
Leistung (gefiltert){m}%0power2% W

; Verbrauch
Tagesverbrauch{m}%2(sml[2]-dval)% kWh
Monatsverbrauch{m}%2(sml[2]-mval)% kWh
Jahresverbrauch{m}%2(sml[2]-yval)% kWh

; Einspeisung (PV)
Tageseinspeisung{m}%2(sml[3]-dval2)% kWh
Monatseinspeisung{m}%2(sml[3]-mval2)% kWh
Jahreseinspeisung{m}%2(sml[3]-yval2)% kWh

; Zeit
Datum{m}%s(2.0day)%.%s(2.0month)%.%s(2.0year)% - %s(2.0hours)%:%s(2.0mins)%:%s(2.0secs)%
Uptime{m}%0(uptime/24/60)%d %0(uptime%(24*60)/60)%h %0(uptime%60)%m
$<div style="margin-left:-30px">

; 1h hochaufgel√∂stes Leistung Diagramm (5s Raster)
$<div id="chart1" style="text-align:center;width:600px;height:400px"></div>
$gc(lt s4h "wr" "Leistung [W]" cstr)
$var options = {
$chartArea:{left:60,right:20,height:'75%%'},
$legend:'none',
;$vAxis:{format:'# W',viewWindow:{max:%vn%}},   optional um per Slider Y-Achse Werte zu begrenzen, suche im Skript nach vn
;$vAxis:{format:'# W',viewWindow:{max:5000}},   optional um Y-Achse Werte auf 5kW zu begrenzen
$vAxis:{format:'# W'},
$hAxis:{slantedTextAngle:45},
$explorer:{actions:['dragToZoom','rightClickToReset']},
$series: {0: {type: 'area'}},
$title:'Leistung 1 Stunde [Watt]'
$};
$gc(e)

; 24h Leistung Diagramm
$<div id="chart2" style="text-align:center;width:600px;height:400px"></div>
$gc(lt s24h "wr" "Leistung [W]" cstr2)
$var options = {
$chartArea:{left:60,right:20,height:'75%%'},
$legend:'none',
;$vAxis:{format:'# W',viewWindow:{max:%vn%}},   optional um per Slider Y-Achse Werte zu begrenzen, suche im Skript nach vn
;$vAxis:{format:'# W',viewWindow:{max:5000}},   optional um Y-Achse Werte auf 5kW zu begrenzen
$vAxis:{format:'# W'},
$hAxis:{slantedTextAngle:45},
$explorer:{actions:['dragToZoom', 'rightClickToReset']},
$series: {0: {type: 'area'}},
$title:'Leistung 24 Stunden [Watt]'
$};
$gc(e)

; Einfacher S√§ulenchart mit 2 Reihen
;$<div id="chart3" style="text-align:center;width:600px;height:400px"></div>
;$gc(c dprod "wr" "kWh" "cnt1" "T√§gliche Einspeisung")

; Tagesverbrauch und Einspeisung Tabelle
%=#daysub
%=#dayprod

; Netzverbrauch und Einspeisung Monat Tabelle
%=#monthsub

$<center><span style="font-size:10px;">
$Version 15.01.2026 (PV2) by ottelo.jimdo.de & gemu2015 (ScriptEngine V%vers%)<br>
$Hinweis: Alle Werte werden um Mitternacht gespeichert!<br>
$4h Diagramm aktualisiert sich alle 5s, 24h Diagramm alle 60s.<br>
$MODIFIZIERT: Historie mit w√§hlbarem Detail-Level<br>
$</span></center></div>


>w Stromz√§hler konfigurieren / Daten sichern / Logging
<button onclick="history.back()">‚¨ÖÔ∏è Zur√ºck</button>
<hr>
<style>
.flex-container{
display:flex;
justify-content:center;
}
.center-flex-horizontally{
width: 270px;
background-color:#f0f0f0;
text-align:left;
padding:20px;
border:2px solid #ccc;
}
.button-group{display:flex;flex-direction:column;align-items:flex-start;gap:10px;}
</style>
so(17+4)
<div class="flex-container">
<div class="center-flex-horizontally">  
<div class=button-group>
<b>üìÑ 2_SML_Script_Chart_PV2.tas</b>
<button type="submit" onclick="if(confirm('Tages/Monats/Jahreswerte und beide Diagramme (4/24h) zur√ºcksetzen? Die Balkendiagramme bleiben erhalten.')){seva(1,'_init');}">üîÑ Z√§hlerwerte initialisieren</button>
<button type="submit" onclick="if(confirm('Balkendiagramme zur√ºcksetzen?')){seva(1,'_init2');}">üìä Reset Balkendiagramme</button>
<button type="button" onclick="seva(1,'_save');alert('Daten wurden gespeichert!');">üíæ Diagrammdaten speichern</button>
</div>
<hr>
<b>‚ö° Stromz√§hlerkonfiguration</b><br>
smlpd("https://raw.githubusercontent.com/ottelo9/tasmota-sml-script/main/script-list-menu/meters" "Stromz√§hlerauswahl (<a href=https://ottelo.jimdofree.com/kontakt>Probleme?)</a><br>" sel)
pd(rxind "RX Pin: " 50 "#gr")
pd(txind "TX Pin: " 50 "#gr")
<span style="font-size:9px">
RX=3 TX=1: Hichi, Stromleser, LesekopfV32, Wattw√§chter<br>
RX=5 TX=4: bitShake</span><br><br>
<button type="button" onclick="location.href='/ufse?file=/sml_meter.def';">üìù Stromz√§hler Script editieren</button><br>
<hr>
<b>üìä Lokales History Logging</b><br><br>
<span>üíΩ Freier Speicher: %fsi(1)% KB</span><br><br>
pd(hlvl "Detail-Level: " 160 "Aus" "Jahr (~0.2KB/10J)" "Monat (~2KB/10J)" "Tag (~60KB/10J)" "Stunde (~1.4MB/10J)")
<br>
<button type="button" onclick="location.href='/ufse?file=/history.csv';">üìÑ History anzeigen</button>
<button type="button" onclick="if(confirm('History l√∂schen?')){fetch('/ufs?delete=history.csv');}">üóëÔ∏è L√∂schen</button>
</div>
</div>


; >M SMARTMETER DESCRIPTOR / Z√ÑHLER AUSWAHL AB 01.12.2025 VIA DROP-DOWN MEN√ú:
; gew√§hlter Z√§hler wird in Tools > Manage Filesystem > sml_meter.def geladen
; TASMOTA (ottelo) IMAGE ab v15.1.0 (07.12.2025) NOTWENDIG
; ODER >M SEKTION MANUELL HINZUF√úGEN ODER sml_deter.def EDITIEREN
