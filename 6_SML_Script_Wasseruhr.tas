>D 40
; IP for external ScriptEditor
IP=192.168.178.31


; (1) Beschreibung:
; Skript Version ganz unten
; Tasmota Script für eine Wasseruhr mit Reed Kontakt(mit Google Chart/Diagramme)
; Gezählt werden 0,1 Liter pro Impuls.
; 2 Liniendiagramme: Stand der letzten 4 Stunden (fein aufgelöst) und Stand der letzten 24h (grob aufgelöst)
; Das 4h Diagramm wird nicht gespeichert und ist nach jedem Neustart neu befüllt
; Tabellen mit Anzeige des Netzbezugs pro Tag und pro Monat
; Tasmota muss mit dem Internet verbunden sein um die aktuelle Uhrzeit via NTP zu erhalten. Ansonsten muss das Skript angepasst werden.

; (2) Anleitung:
; (a) Variable factor=10000 an eure Wasseruhr anpassen. 10000 bedeutet 1 Puls = 0.0001m³
; (b) Script übertragen
; Ich empfehle das Script mit dem ext. Editor zu "komprimieren" (Kommentare entfernen). Script laden -> Window -> Export to documents folder. Dann einfach in Tasmota einfügen.
; ScriptEditor runterladen um dieses Skript auf den ESP zu übertragen. Der Editor entfernt alle Kommentare und leere Zeilen
; https://tasmota.github.io/docs/Scripting-Language/#optional-external-editor
; IP eures ESPs oben im Script eingeben, damit der Editor das Skript korrekt senden kann
; Hinweis Dateiformat Scriptdatei: UTF8, LF (\n) als Zeilenende (Unix EOL), damit der ext. Editor keine doppelten Leerzeichen anzeigt. Einstellbar in Notepad++ (Edit->EOL->Unix).
; (c) Initialisieren
; Folgendes in die Console eingeben um euren aktuellen Stand der Wasseruhr in m³ zu setzen: sensor53 c1 m3      m3 euer Stand in m³ multipliziert mit dem Faktor (siehe factor), z.B. 1070000 für 107m³ wenn factor=10000
; Damit die Diagramme und Tabellen beim ersten Einrichten funktionieren folgendes in die Console auf eurem ESP eingeben (; am Anfang entfernen) - Achtung alle bereits gespeicherten Diagrammdaten werden gelöscht!
; Nachdem ihr euren aktuellen Zählerstand eingegeben habt müsst ihr kurz warten, bis sich der Tageswert aktualisiert hat, dann weitermachen:
;backlog script>dval=WaterCon; script>dcon={0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0};
;backlog script>mval=WaterCon; script>mcon={0 0 0 0 0 0 0 0 0 0 0 0};
;backlog script>yval=WaterCon;      Wasseruhr Stand beim Jahreswechsel in m³. Statt WaterCon koennt ihr dort auch euren alten Stand eingeben, falls ihr diesen am Ende des Jahres oder Neujahr notiert habt.
;script >=#rst24h
;script >svars
; (d) Um später fehlende oder falsche Werte in den Tabellen zu ändern, können folgende Befehle in der Console eingegeben werden:
;script?var        beliebige Variable auslesen z.B. script?dcon[1] (erster Tag in Monatstabelle lesen)
;script>var=1     beliebige Variable schreiben z.B. script >dcon[1]=10 (erster Tag in Monatstabelle = 10 kWh)
; (e) Um eure Diagrammdaten zu exportieren/sichern könnt ihr folgendes hintereinander eingeben. Die ausgegebenen Daten einfach markieren und in eine Textdatei kopieren:
;script?dval   script?dcon   script?mval   script?mcon   script?yval
; Zum Importieren müssen sie dann mit z.B. script>dcon={x x x x ...} eingeben werden. Am Ende speichern.


; -- ARRAYS --
; 24h chart
M:p:sday=0 288
; Tagesverbrauch Tabelle 1-31
M:p:dcon=0 31
; Monatsverbrauch Tabelle 1-12
M:p:mcon=0 12
; 4h Chart, Neuer Wert alle 30s
M:s4h=0 481
; -- VARS --
; Water Consumption / Wasserverbrauch [m³] Der Wert wird hochgezählt
WaterCon=0
; PermVars (max:1536bytes=384vars): Tages, Monats, Jahres Verbrauch
p:mval=0
p:dval=0
p:yval=0
p:da=1
t:t1=3
t:t2=30
t:t3=60
tmp=0
m5=0
cstr="cnt0/12"
cstr2="cnth0/120"
utm="00d 00h 00m"
hr=0
save=0
;10000: 1 Puls = 0.0001m³ für >M Sektion
factor=10000


; -- BOOT --
>B
WaterCon=pc[1]/factor
tmp=is(0 "Jan|Feb|Mär|Apr|Mai|Jun|Jul|Aug|Sep|Okt|Nov|Dez|")
;Deaktiviere MQTT beim Start, verhindert das falsche Werte gesendet werden
smlj=0


; -- SUBS --
#init
dval=WaterCon
for tmp 1 dcon[-1] 1
	dcon[tmp]=0
next
mval=WaterCon
for tmp 1 mcon[-1] 1
	mcon[tmp]=0
next
yval=WaterCon
for tmp 1 sday[-1] 1
	sday[tmp]=0
next
->Backlog2 otaurl KEIN OTA UPGRADE VIA WEBSERVER!
->Backlog2 Timezone 99;TimeStd 0,0,10,1,3,60;TimeDst 0,0,3,1,2,120
svars

; Tagesverbrauch Tabelle
#daysub
if wm>0 {
wcs <div id="day" style="text-align:center;width:600px;height:400px"></div>
wcs <script language="JavaScript">function drawChart(){
wcs var cssc={'headerRow':'hRow','rowNumberCell':'hCol','tableCell':'tCell'};
wcs var data=google.visualization.arrayToDataTable([['Tag','Verbrauch [m³]',{role: 'style'}],
for tmp 1 dcon[-1] 1
	if (tmp==day) {
		wcs [%tmp%,%dcon[tmp]%,'red'],
	}
	if (tmp<day) {
		wcs [%tmp%,%dcon[tmp]%,'green'],
	}
	if (tmp>day) {
		wcs [%tmp%,%dcon[tmp]%,''],
	}
next
wcs ]);
wcs var options={chartArea:{left:40,right:30,height:'75%%'},legend:'none',title:'Tagesverbräuche (Monat %is[month]%)',vAxis:{format:'# m³'},hAxis:{title:'Tag',ticks:[1,5,10,15,20,25,30]}};
wcs var chart=new google.visualization.ColumnChart(document.getElementById('day'));
wcs chart.draw(data,options);}google.charts.setOnLoadCallback(drawChart);</script>
}

; Netzverbrauch Monat Tabelle
#monthsub
if wm>0 {
wcs <div id="month" style="text-align:center;width:600px;height:400px"></div>
wcs <script language="JavaScript">function drawChart(){
wcs var cssc={'headerRow':'hRow','rowNumberCell':'hCol','tableCell':'tCell'};
wcs var data=google.visualization.arrayToDataTable([['Monat','Verbrauch [m³]',{role: 'style'}],
for tmp 1 12 1
	if (tmp<month) {
		wcs ['%is[tmp]%',%mcon[tmp]%,'green'],
	}
	if (tmp==month) {
		wcs ['%is[tmp]%',%mcon[tmp]%,'red'],
	}
	if (tmp>month) {
		wcs ['%is[tmp]%',%mcon[tmp]%,'blue'],
	}
next
wcs ]);
wcs var options={chartArea:{left:40,right:40,height:'75%%'},legend:'none',title:'Verbräuche (Jahr %0(year-1)%/%0year%)',vAxis:{format:'# m³'}};
wcs var chart=new google.visualization.ColumnChart(document.getElementById('month'));
wcs chart.draw(data,options);}google.charts.setOnLoadCallback(drawChart);</script>
}


; -- JEDE SEKUNDE --
>S
; Daten sofort speichern Button
if (save==1) {
	save=0
	svars
}
; Warte auf NTP
if (year<2020) {
	print auf NTP warten
	break
}
; Starte MQTT erst jetzt
smlj=1

; alle 30s
if (t2==0) {
	t2=30
	; kopiere Netzbezug [m³] vom Zaehler (aus counter 1), dividiere durch den Faktor
	WaterCon=pc[1]/factor
	; 4h chart. idx wird automatisch gesetzt
	s4h=WaterCon

	; Setze 4h Chart X-Achse alle 30s => /120 Werte pro Stunde. Arraygrösse = 480
	; tasmota calc [hh:mm] via cnthX/Y. mm = X%Y * 60/Y. hh = X/Y
	cstr2="cnth"+s(1.0((((hours+20)*120)+(mins*2)+(int(secs/30)))%2880+1))+"/120"
}

; alle 60s
if (t3==0) {
	t3=60
	hr=hours
	;Zeit seit letztem Boot
	utm=s(2.0(int(uptime/1440)))+"d "+s(2.0(int(uptime/60)%24))+"h "+s(2.0(uptime%60))+"m"

	; Tagesverbrauch [m³]
	dcon[day]=WaterCon-dval
	; Monatsverbrauch [m³]
	mcon[month]=WaterCon-mval
	
	;=>publish stat/%topic%/script/consday %2(dcon[day])%
	;=>publish stat/%topic%/script/consmonth %2(mcon[month])%
	;=>publish stat/%topic%/script/consyear %2(WaterCon-yval)%

	; 24h Chart. Zählerstand der letzten 5min => ins Array[1-288]
	m5=int((((hours*60)+mins)/5)+1)
	; set idx (pointer starts from 0)
	sday[0]=m5
	if (chg[m5]>0) {
		sday[m5]=WaterCon
	}

	; Aktualisiere 24h Chart alle 5Min => Arraygröße 288
	; tasmota calc [hh:mm] via cnthX/Y. mm = X%Y * 60/Y. hh = X/Y
	cstr="cnth"+s(1.0(((hours)*12)+int(mins/5))+1)+"/12"

	; Tagesverbrauch Berechnung um Mitternacht
	if ((chg[hr]>0) and (hr==0)) {
		if (day>1) {
			da=day
		} else {
		    ; Monatswechsel
			for tmp (da+1) 31 1
				dcon[tmp]=0
			next
			; monthly values
			mval=WaterCon
		}
		if (day*month==1) {
			; Jahreswechsel
			yval=WaterCon
		}
		; daily values
		dval=WaterCon
		; nur 1x um Mitternacht speichern
		svars
	}
}


; WEB INTERFACE
>W
; Auto reload
;$<script> setTimeout("location.reload(true);",60000); </script>

;Tasmota Buttons ausblenden. Um an die Menüs zu kommen z.B. http://tasmota-ip/mn? eingeben
;<style>form button{display:none}</style>

; web button
bu(save "gespeichert!" "Daten sofort speichern")

; Verbrauch
Tagesverbrauch{m}%4(WaterCon-dval)% m³
Monatsverbrauch{m}%2(WaterCon-mval)% m³
Jahresverbrauch{m}%2(WaterCon-yval)% m³

; Zeit
Datum{m}%s(2.0day)%.%s(2.0month)%.%s(2.0year)% - %s(2.0hours)%:%s(2.0mins)%:%s(2.0secs)%
Uptime{m}%utm%
$<div style="margin-left:-30px">

; 4h Leistung Diagramm
$<div id="chart1" style="text-align:center;width:600px;height:400px"></div>
$gc(lt s4h "wr" "Verbrauch [m³]" cstr2)
$var options = {
$chartArea:{left:60,right:20,height:'75%%'},
$legend:'none',
$vAxis:{format:'# m³'},
$hAxis:{slantedTextAngle:45},
$explorer:{actions:['dragToZoom','rightClickToReset']},
$series: {0: {type: 'area'}},
$title:'Verbrauch 4 Stunden [m³]'
$};
$gc(e)

; 24h Leistung Diagramm
$<div id="chart2" style="text-align:center;width:600px;height:400px"></div>
$gc(lt sday "wr" "Verbrauch [m³]" cstr)
$var options = {
$chartArea:{left:60,right:20,height:'75%%'},
$legend:'none',
$vAxis:{format:'# m³'},
$hAxis:{slantedTextAngle:45},
$explorer:{actions:['dragToZoom', 'rightClickToReset']},
$series: {0: {type: 'area'}},
$title:'Verbrauch 24 Stunden [m³]'
$};
$gc(e)

; Tagesverbrauch Tabelle
%=#daysub

; Netzverbrauch Monat Tabelle
%=#monthsub

$<center><span style="font-size:10px;">
$Version 29.09.2025 (Wasseruhr) by ottelo.jimdo.de<br>
$Credits to gemu2015 (Tasmota Script Dev)<br>
$Hinweis: Die Daten (ohne 4h Chart) werden um Mitternacht gespeichert!<br>
$Der Restart Button speichert ebenfalls die Daten.<br>
$</span></center></div>


;-- SML --
; https://tasmota.github.io/docs/Smart-Meter-Interface/#meter-metrics
>M 1
;1=meter1, 21=GPIO21, c=Counter, 1=counter-with-pullup -50=debounce-time-ms and irq counter
+1,21,c,1,-50,
;1-0:1.8.0*255 for counter, %factor% = Faktor z.B. 10000 = 0.0001m³, 4 = Dezimalstellen
1,1-0:1.8.0*255(@%factor%,Wasseruhr,m³,Wasseruhr,4)
#