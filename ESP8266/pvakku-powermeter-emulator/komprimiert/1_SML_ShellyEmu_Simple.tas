>D 250
;1_SML_ShellyEmu_Simple.tas
p:dval=0
p:dval2=0
p:mval2=0
p:mval=0
p:yval=0
p:yval2=0
p:pwroffset=0
p:pwrforce=0
p:throttle=1
t:t1=60
tmp=0
save=0
hr=0
res=0
c1p=0
c2p=0
c3p=0
c1c=0
c2c=0
c3c=0
cpwr=0
str=""
mstr1=""
mstr2=""
mstr3=""
header=""
once=0
>B
->SetOption57 0
->sensor53 r
smlj=0
header="{\"id\":0,\"src\":\"shellypro3em-"+maca+"\",\"result\":"
throttle=mp(throttle <1 1 >60 60)
pwroffset=mp(pwroffset <-200 -200 >200 200)
#init
dval=sml[2]
dval2=sml[3]
mval=sml[2]
mval2=sml[3]
yval=sml[2]
yval2=sml[3]
->Backlog2 otaurl KEIN OTA UPGRADE VIA WEBSERVER!
->Backlog2 Timezone 99;TimeStd 0,0,10,1,3,60;TimeDst 0,0,3,1,2,120
svars
>ah
res=won(1 "/rpc/*")
>on1
str=warg
if (str!="") {
res=ins(str "EM.GetStatus")
if res>=0 {
=#getstat
tmp=sl(mstr1)+sl(mstr2)
=#header
wcs so(4)
wcs %header%
wcs %mstr1%
wcs %mstr2%
wcf
break
}
res=ins(str "Shelly.GetStatus")
if res>=0 {
=#getshellystat
tmp=sl(mstr1)+sl(mstr2)+sl(mstr3)
=#header
wcs so(4)
wcs %header%
wcs %mstr1%
wcs %mstr2%
wcs %mstr3%
wcf
break
}
print unknown rpc request: %str%
}
#getstat
dp(0 2)
mstr1="{\"id\":0,\"a_act_power\":"+s(c1p)+",\"b_act_power\":"+s(c2p)+","
mstr2="\"c_act_power\":"+s(c3p)+",\"total_act_power\":"+s(cpwr)+"}}"
#getshellystat
dp(0 2)
mstr1="{\"em:0\":{\"id\":0,\"a_act_power\":"+s(c1p)+",\"b_act_power\":"+s(c2p)+",\"c_act_power\":"+s(c3p)+",\"total_act_power\":"+s(cpwr)+"},"
mstr2="\"emdata:0\":{\"id\":0,\"a_total_act_energy\":"+s(sml[2])+",\"a_total_act_ret_energy\":"+s(sml[3])+",\"b_total_act_energy\":0,\"b_total_act_ret_energy\":0,\"c_total_act_energy\":0,\"c_total_act_ret_energy\":0,"
mstr3="\"total_act\":"+s(sml[2])+",\"total_act_ret\":"+s(sml[3])+"}}"
#header
header="HTTP/1.1 200 OK\r\nServer: ShellyHTTP/1.0.0\r\nContent-Type: application/json\r\nContent-Length: "+s(0tmp)+"\r\nConnection: close\r\n\r\n"
>F
str=udp(1)
if (str!="") {
header="{\"id\":0,\"src\":\"shellypro3em-"+maca+"\",\"result\":"
res=ins(str "EM.GetStatus")
if (res>=0) {
=#getstat
udp(2 header mstr1 mstr2)
break
}
res=ins(str "Shelly.GetStatus")
if res>=0 {
=#getshellystat
mstr3+="}"
udp(2 header mstr1 mstr2)
udp(2 mstr3)
break
}
}
>S
if (save==1) {
save=0
svars
}
if ((year<2020) or (sml[2]==0)) {
print auf NTP/Zähler warten
break
}
smlj=1
if (t1==0) {
t1=60
hr=hours
if ((chg[hr]>0) and (hr==0)) {
dval=sml[2]
dval2=sml[3]
if (day==1) {
mval=sml[2]
mval2=sml[3]
}
if (day*month==1) {
yval=sml[2]
yval2=sml[3]
}
svars
}
}
if (once==0) {
mdns("shellypro3em-" "-" "shelly")
udp(0 1010)
once=1
}
if (upsecs%throttle==0) {
tmp=sml[1]-cpwr
if (tmp>500) {
cpwr=sml[1]-(tmp/2)
} else {
cpwr=sml[1]-pwroffset
}
c1p=cpwr
c1c=c1p/230
if (pwrforce>0) {
=#getstat
udp(2 header mstr1 mstr2)
}
>W
bu(save "gespeichert!" "Daten sofort speichern")
Leistung (an Marstek Akku){m}%0cpwr% W
Tagesverbrauch{m}%2(sml[2]-dval)% kWh
Monatsverbrauch{m}%2(sml[2]-mval)% kWh
Jahresverbrauch{m}%2(sml[2]-yval)% kWh
Tageseinspeisung{m}%2(sml[3]-dval2)% kWh
Monatseinspeisung{m}%2(sml[3]-mval2)% kWh
Jahreseinspeisung{m}%2(sml[3]-yval2)% kWh
Uptime{m}%0uptime% min
$<span style="font-size:10px;">
$<b>Einstellungen / Status</b><br>
$Ändern der [Variable] via Console: script>var=x<br>
$- Update der Werte verzögern [throttle]: %0throttle% s<br>
$- Offset für Nulleinspeisung [pwroffset]: %0pwroffset% W<br>
$- Datenübertragung erzwingen [pwrforce]: %0pwrforce% s<br>
$Scriptspeicher: %0slen% chars / 4096(1M) 8192(4M)
$<hr>
$<center>Version 21.02.2026 (ShellyEmuSimple) by ottelo.jimdo.de<br>
$Credits to gemu2015 (Tasmota Script Dev)<br>
$</span></center>
>M 1
+1,3,s,0,9600,MT175,1
1,77070100100700ff@1,Leistung,W,Power_curr,0
1,77070100010800ff@1000,Verbrauch,KWh,Total_in,2
1,77070100020800ff@1000,Einspeisung,KWh,Total_out,2
#
