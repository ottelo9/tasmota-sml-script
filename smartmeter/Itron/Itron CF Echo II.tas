>D
done=0
wkup=1

>B
smlj=0
->sensor53 r

>BS
=#readmeter

>S
if sb(tstamp 14 2)=="30"
and done==0
then
=#readmeter
done=1
print done set
endif
if sb(tstamp 14 2)=="31"
and done==1
then
done=0
print done reset
endif
if (sml[2]>0
and sml[10]>0)
and smlj==0
then
smlj=1
print enabled MQTT
endif
if (sml[2]==0
or sml[10]==0)
and smlj==1
then
smlj=0
print disabled MQTT
endif
#readmeter
print wakeup start
;set serial protocol
sml(-1 1 "2400:8N1")
;send 0x55 for 2,2 seconds with 8N1 (53x), 2400 baud (wakeup sequence)
for wkup 1 53 1
sml(1 1 "55555555555555555555")
next
print wakeup end
wkup=1
print wait for the meter
delay(350)
;switch serial protocol
sml(-1 1 "2400:8E1")
print request data
;request data with "105B005B16"
sml(1 1 "105BFE5916")

>M 1
+1,5,rE1,0,2400,WAERME,4
1,=so3,16
1,=soC,1024,3
1,0C78bcd8@1,Fabrication number,no,fabrication_no,0
1,0C78xxxxxxxx0406uuUUuuUUs@1000,Total energy,MWh,total_energy,3
1,0406xxxxxxxx0C14bcd8@100,Total volume,m³,total_volume,2
1,0C14xxxxxxxx0B2Dbcd6@10,Current power,kW,current_power,2
1,0B2Dxxxxxx0B3Bbcd6@1000,Current volume flow,m³/h,current_volume_flow,3
1,0B3Bxxxxxx0A5Abcd4@10,Flow temperature,°C,temp_flow,1
1,0A5Axxxx0A5Ebcd4@10,Return temperature,°C,temp_return,1
1,0A5Exxxx0B61bcd6@100,Temperature difference,°C,temp_diff,2
1,0B61xxxxxx046DuuUUuuUUs@1,Date and time,t,meter_time,0
1,046Dxxxxxxxx0227uuUU@1,Operating time days,d,meter_days,0
1,0227xxxx09FD0Ebcd2@1,Firmware version,v,firmware_version,0
1,0227xxxx09FD0Exx09FD0Fbcd2@1,Software version,v,software_version,0
#