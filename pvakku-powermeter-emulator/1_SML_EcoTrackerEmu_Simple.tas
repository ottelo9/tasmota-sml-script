>D 40
; Einfaches EcoTracker Emulator Script für ESP8266/ESP32 und Tasmota Image von ottelo
; Emuliert EcoTracker für Marstek Akkus (Jupiter, Venus, B2500) und Hoymiles (MS-A2)
; Anleitung https://ottelo.jimdofree.com/stromz%C3%A4hler-auslesen-tasmota/#13a
; Script basiert auf https://github.com/gemu2015/Sonoff-Tasmota/blob/universal/tasmota/scripting/shelly_emu_script.tas
; Script ist für ein Wifi Tasmota ESP Lesekopf für Stromzähler gedacht, z.B. HichiV1/V2, Bitshake, usw.
; Der Text kann einfach so in den internen Editor von Tasmota eingefügt werden. Beim ESP8266 sollten alle Kommentare entfernen werden.
; Die Marstek App sucht nach dem EcoTracker via mdns, der Emulator meldet sich als ecotracker-<mac>
; !! Es darf kein Passwortschutz in Tasmota aktiv sein !!
; Der Datenaustausch erfolgt dann via REST API https://everhome.cloud/en/developer/ecotracker alle paar Sek (~13s beim Jupiter C Plus)
; Zum Test einfach http://IP-TASMOTA/v1/json eingeben
res=0
tmp=0
cpwr=0
kwhin=0
kwhout=0
once=0
header=""
json=""

>B
=>sensor53 r

; bei Änderung in >ah muss Tasmota neugestartet werden
>ah
; Ecotracker
res=won(1 "/v1/json")

>on1
;print Ecotracker
dp(0 1)
json="{\"power\":"+s(cpwr)+",\"powerAvg\":"+s(cpwr)+",\"energyCounterIn\":"+s(kwhin)+",\"energyCounterOut\":"+s(kwhout)+"}"
tmp=sl(json)
dp(0 0)
header="HTTP/1.1 200 OK\r\nContent-Length: "+s(tmp)+"\r\nContent-Type: application/json\r\n\r\n"
wcs so(4)
wcs %header%
wcs %json%
wcf

>S
; starte MQTT erst wenn der Stromzähler sendet
if (sml[2]>0) {
smlj=1
}
; auf NTP warten
if year<2000 {
break
}
; starte mDNS damit Marstek den emulierten EcoTracker findet
if once==0 {
res=mdns("ecotracker-" "-" "everhome")
once=1
}
; Peaks dämpfen (z.B. Kühlschrank)
tmp=sml[1]-cpwr
if (tmp>500) {
cpwr=sml[1]-(tmp/2)
} else {
; reduziere die Leistung um 50W, damit die Regelung nicht um 0W, sondern um 50W herum schwingt (reduziert Einspeisung)
cpwr=sml[1]-50
}
; Netzbezug und Einspeisung
kwhin=sml[2]*1000
kwhout=sml[3]*1000


>W
Uptime{m}%0uptime% min
Leistung an Marstek{m}%0cpwr% W
$<center><span style="font-size:10px;">
$Version 08.09.2025 (EcoTrackerEmuSimple) by ottelo.jimdo.de<br>
$Credits to gemu2015 (Tasmota Script Dev)<br>
$</span></center>


; SML Sektion an euren Stromzähler anpassen
; Anleitung: https://ottelo.jimdofree.com/stromz%C3%A4hler-auslesen-tasmota/#4c
; Reihenfolge nicht ändern: 1=(Gesamt)Leistung sml[1] 2=Verbrauch sml[2] 3=Einspeisung sml[3]
; https://tasmota.github.io/docs/Smart-Meter-Interface/#meter-definition
>M 1
; Beispiel MT175 mit HichiV2
+1,3,s,0,9600,MT175,1
1,77070100100700ff@1,Leistung,W,Power_curr,0
1,77070100010800ff@1000,Verbrauch,KWh,Total_in,2
1,77070100020800ff@1000,Einspeisung,KWh,Total_out,2
#
