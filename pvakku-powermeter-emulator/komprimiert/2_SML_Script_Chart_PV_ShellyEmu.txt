>D 250
M:p:sday=0 288
M:p:dcon=0 31
M:p:dprod=0 31
M:p:mcon=0 24
M:s4h=0 480
EnFrGrid=0
EnToGrid=0
power=0
p:mval=0
p:dval=0
p:mval2=0
p:dval2=0
p:yval=0
p:yval2=0
p:da=1
tmp=0
m5=0
cstr="cnt0/12"
cstr2="cnth0/120"
utm="00d 00h 00m"
avgv=0
avgv2=0
avgvc=0
avgvc2=0
hour=0
yr=0
swesp=0
swespflg=0
power2=0
save=0
res=0
c1p=0
c2p=0
c3p=0
c1c=0
c2c=0
c3c=0
cpwr=0
str=""
tstr=""
mstr1=""
mstr2=""
mstr3=""
header=""
once=0
>B
->Backlog2 otaurl KEIN OTA UPGRADE VIA WEBSERVER!
->Backlog2 Timezone 99;TimeStd 0,0,10,1,3,60;TimeDst 0,0,3,1,2,120
=>sensor53 r
tmp=is(0 "Jan|Feb|Mär|Apr|Mai|Jun|Jul|Aug|Sep|Okt|Nov|Dez|")
smlj=0
res=ct(1 1000 1)
#rst24h
for tmp 1 sday[-1] 1
sday[tmp]=0
next
#daysub
if wm>0 {
wcs <div id="day" style="text-align:center;width:600px;height:400px"></div>
wcs <script language="JavaScript">function drawChart(){
wcs var cssc={'headerRow':'hRow','rowNumberCell':'hCol','tableCell':'tCell'};
wcs var data=google.visualization.arrayToDataTable([['Tag','Energie [kWh]',{role: 'style'}],
for tmp 1 dcon[-1] 1
if (tmp==day) {
wcs [%tmp%,%dcon[tmp]%,'red'],
}
if (tmp<day) {
wcs [%tmp%,%dcon[tmp]%,'green'],
}
if (tmp>day) {
wcs [%tmp%,%dcon[tmp]%,''],
}
next
wcs ]);
wcs var options={chartArea:{left:40,width:'86%%'},legend:'none',title:'Tagesverbräuche (Monat %is[month]%)',vAxis:{format:'# kWh'},hAxis:{title:'Tag',ticks:[1,5,10,15,20,25,30]}};
wcs var chart=new google.visualization.ColumnChart(document.getElementById('day'));
wcs chart.draw(data,options);}google.charts.setOnLoadCallback(drawChart);</script>
}
#dayprod
if wm>0 {
wcs <div id="dayp" style="text-align:center;width:600px;height:400px"></div>
wcs <script language="JavaScript">function drawChart(){
wcs var cssc={'headerRow':'hRow','rowNumberCell':'hCol','tableCell':'tCell'};
wcs var data=google.visualization.arrayToDataTable([['Tag','Energie [kWh]',{role: 'style'}],
for tmp 1 dprod[-1] 1
if (tmp==day) {
wcs [%tmp%,%dprod[tmp]%,'red'],
}
if (tmp<day) {
wcs [%tmp%,%dprod[tmp]%,'green'],
}
if (tmp>day) {
wcs [%tmp%,%dprod[tmp]%,''],
}
next
wcs ]);
wcs var options={chartArea:{left:40,width:'86%%'},legend:'none',title:'Tageseinspeisung (Monat %is[month]%)',vAxis:{format:'# kWh'},hAxis:{title:'Tag',ticks:[1,5,10,15,20,25,30]}};
wcs var chart=new google.visualization.ColumnChart(document.getElementById('dayp'));
wcs chart.draw(data,options);}google.charts.setOnLoadCallback(drawChart);</script>
}
#monthsub
if wm>0 {
wcs <div id="month" style="text-align:center;width:600px;height:400px"></div>
wcs <script language="JavaScript">function drawChart(){
wcs var cssc={'headerRow':'hRow','rowNumberCell':'hCol','tableCell':'tCell'};
wcs var data=google.visualization.arrayToDataTable([['Monat','Verbrauch [kWh]','Einspeisung[kWh]'],
for tmp 1 12 1
if (tmp<month) {
wcs ['%is[tmp]%',%mcon[tmp]%,%mcon[tmp+12]%],
}
if (tmp==month) {
wcs ['%is[tmp]%',%mcon[tmp]%,%mcon[tmp+12]%],
}
if (tmp>month) {
wcs ['%is[tmp]%',%mcon[tmp]%,%mcon[tmp+12]%],
}
next
wcs ]);
wcs var options={series:{0:{targetAxisIndex:0},1:{targetAxisIndex:1}},chartArea:{left:40,width:'80%%'},legend:'none',title:'Verbräuche / Einspeisungen (Jahr %0(year-1)%/%0year%)',vAxes:{0:{format:'# kWh'},1:{format:'# kWh'}}};
wcs var chart=new google.visualization.ColumnChart(document.getElementById('month'));
wcs chart.draw(data,options);}google.charts.setOnLoadCallback(drawChart);</script>
}
#htph
mstr1="HTTP/1.1 200 OK\r\nContent-type: application/json\r\n\r\n"
#getcfg
mstr1="{\"id\":0,\"name\":null,\"blink_mode_selector\":\"active_energy\",\"phase_selector\":\"a\",\"monitor_phase_sequence\":true,\"ct_type\":\"120A\"}}"
#getdefi
mstr1="{\"name\":\""+tstr+"\",\"id\":\""+tstr+"\",\"mac\":\""+maca+"\",\"slot\":1,\"model\":\"SPEM-003CEBEU\","
mstr2="\"gen\":2,\"fw_id\":\"20241011-114455/1.4.4-g6d2a586\","
mstr2+="\"ver\":\"1.4.4\",\"app\":\"Pro3EM\",\"auth_en\":0,\"profile\":\"triphase\"}}"
#getstat
dp(0 2)
mstr1="{\"id\":0,\"a_current\":"+s(c1c)+",\"a_voltage\":230,\"a_act_power\":"+s(c1p)+",\"a_aprt_power\":"+s(c1p)+",\"a_pf\":1,\"a_freq\":50,"
mstr1+="\"b_current\":"+s(c2c)+",\"b_voltage\":230,\"b_act_power\":"+s(c2p)+",\"b_aprt_power\":"+s(c2p)+",\"b_pf\":1,\"b_freq\":50,"
mstr2="\"c_current\":"+s(c3c)+",\"c_voltage\":230,\"c_act_power\":"+s(c3p)+",\"c_aprt_power\":"+s(c3p)+",\"c_pf\":1,\"c_freq\":50,"
mstr2+="\"total_current\":"+s(c1c+c2c+c3c)+",\"total_act_power\":"+s(cpwr)+",\"total_aprt_power\":"+s(cpwr)+"}}"
#egetstat
dp(0 2)
mstr1="{\"id\":0,\"a_total_act_energy\":"+s(c1p)+",\"a_total_act_ret_energy\":"+s(c1p)+",\"b_total_act_energy\":"+s(c2p)+",\"b_total_act_ret_energy\":"+s(c2p)
mstr1+=",\"c_total_act_energy\":"+s(c3p)+",\"c_total_act_ret_energy\":"+s(c3p)+",\"total_act\":"+s(cpwr)+",\"total_act_ret\":"+s(cpwr)+"}}"
>ah
res=won(1 "/rpc/*")
res=won(2 "/status")
>on1
print SHELLY RPC RQ
str=warg
res=ins(str "EM.GetStatus")
if res>=0 {
wcs so(4)
=#htph
wcs %mstr1%
wcs %header%
=#getstat
wcs %mstr1%
wcs %mstr2%
wcf
break
}
res=ins(str "Shelly.GetDeviceInfo")
if res>=0 {
wcs so(4)
=#htph
wcs %mstr1%
=#getdefi
wcs %header%
wcs %mstr1%
wcs %mstr2%
wcf
break
}
res=ins(str "EM.GetConfig")
if res>=0 {
wcs so(4)
=#htph
wcs %mstr1%
=#getcfg
wcs %header%
wcs %mstr1%
wcf
break
}
res=ins(str "EMData.GetStatus")
if res>=0 {
wcs so(4)
=#htph
wcs %mstr1%
=#egetstat
wcs %header%
wcs %mstr1%
wcf
break
}
print UNKNOWN RQ: %str%
>on2
print SHELLY STATUS RQ
wcs so(4)
=#htph
wcs %mstr1%
dp(0 2)
wcs {"Power": %cpwr%,"E_in":%EnFrGrid%,"E_out":%EnToGrid%}
wcf
>t1
if once==0 {
res=mdns("shellypro3em-" "-" "shelly")
res=udp(0 1010)
tstr="shellypro3em-"+maca
header="{\"id\":0,\"src\":\""+tstr+"\",\"result\":"
once=1
}
tmp=sml[1]-cpwr
if (tmp>500) {
cpwr=sml[1]-(tmp/2)
} else {
cpwr=sml[1]-50
}
c1p=cpwr
c1c=c1p/230
str=udp(1)
if str!="" {
res=ins(str "EM.GetStatus")
if res>=0 {
=#getstat
udp(2 header mstr1 mstr2)
break
}
res=ins(str "Shelly.GetDeviceInfo")
if res>=0 {
=#getdefi
udp(2 header mstr1 mstr2)
break
}
res=ins(str "EM.GetConfig")
if res>=0 {
=#getcfg
udp(2 header mstr1)
break
}
res=ins(str "EMData.GetStatus")
if res>=0 {
=#egetstat
udp(2 header mstr1)
break
}
} else {
=#getstat
udp(2 header mstr1 mstr2)
}
>S
if (save==1) {
save=0
svars
print Daten wurden gespeichert
}
if (sml[2]>0) {
smlj=1
}
if (year<2020) {
print NTP not ready
break
}
power=sml[1]
power2=(0.9*power2)+((1-0.9)*power)
if (upsecs%3==0) {
avgv+=power
avgv2+=power
avgvc+=1
avgvc2+=1
=>publish stat/%topic%/script/power2 %power2%
if (swesp==1) {
if ((power2<-300) and (swespflg==0)) {
swespflg=1
}
if ((power2>100) and (swespflg==1)) {
swespflg=0
}
}
}
if (upsecs%30==0) {
s4h=avgv/avgvc
avgv=0
avgvc=0
cstr2="cnth"+s(1.0((((hours+20)*120)+(mins*2)+(int(secs/30)))%2880+1))+"/120"
}
if (upsecs%60==0) {
hour=hours
yr=year
utm=s(2.0(int(uptime/1440)))+"d "+s(2.0(int(uptime/60)%24))+"h "+s(2.0(uptime%60))+"m"
EnFrGrid=sml[2]
EnToGrid=sml[3]
dcon[day]=EnFrGrid-dval
mcon[month]=EnFrGrid-mval
dprod[day]=EnToGrid-dval2
mcon[month+12]=EnToGrid-mval2
m5=int((((hours*60)+mins)/5)+1)
sday[0]=m5
if (chg[m5]>0) {
sday[m5]=int(avgv2/avgvc2)
avgv2=0
avgvc2=0
}
cstr="cnth"+s(1.0(((hours)*12)+int(mins/5))+1)+"/12"
if ((chg[hour]>0) and (hour==0)) {
if (day>1) {
da=day
} else {
for tmp (da+1) 31 1
dprod[tmp]=0
dcon[tmp]=0
next
mval=EnFrGrid
mval2=EnToGrid
}
if (chg[yr]>0) {
yval=EnFrGrid
yval2=EnToGrid
}
dval=EnFrGrid
dval2=EnToGrid
svars
}
}
>W
bu(save "gespeichert!" "Daten sofort speichern")
Leistung (gefiltert){m}%0power2% W
Leistung (an Marstek Akku){m}%0cpwr% W
Tagesverbrauch{m}%2(EnFrGrid-dval)% kWh
Monatsverbrauch{m}%2(EnFrGrid-mval)% kWh
Jahresverbrauch{m}%2(EnFrGrid-yval)% kWh
Tageseinspeisung{m}%2(EnToGrid-dval2)% kWh
Monatseinspeisung{m}%2(EnToGrid-mval2)% kWh
Jahreseinspeisung{m}%2(EnToGrid-yval2)% kWh
Datum{m}%s(2.0day)%.%s(2.0month)%.%s(2.0year)% - %s(2.0hours)%:%s(2.0mins)%:%s(2.0secs)%
Uptime{m}%utm%
$<div style="margin-left:-20px">
$<div id="chart1" style="text-align:center;width:600px;height:400px"></div>
$gc(lt s4h "wr" "Leistung [W]" cstr2)
$var options = {
$chartArea:{left:60,width:'83%%'},
$legend:'none',
$vAxis:{format:'# W'},
$explorer:{actions:['dragToZoom', 'rightClickToReset']},
$series: {0: {type: 'area'}},
$title:'Verbrauch 4 Stunden [Watt]'
$};
$gc(e)
$<div id="chart2" style="text-align:center;width:600px;height:400px"></div>
$gc(lt sday "wr" "Leistung [W]" cstr)
$var options = {
$chartArea:{left:60,width:'83%%'},
$legend:'none',
$vAxis:{format:'# W'},
$explorer:{actions:['dragToZoom', 'rightClickToReset']},
$series: {0: {type: 'area'}},
$title:'Verbrauch 24 Stunden [Watt]'
$};
$gc(e)
%=#daysub
%=#dayprod
%=#monthsub
$<center><span style="font-size:10px;">
$Version 28.08.2025 (PV+ShellyEmu) by ottelo.jimdo.de<br>
$Credits to gemu2015(Tasmota Script Dev)<br>
$Hinweis: Die Daten (ohne 4h Chart) werden um Mitternacht gespeichert!<br>
$Der Restart Button speichert ebenfalls die Daten.<br>
$</span></center></div>
>M 1
+1,5,s,0,9600,MT175,4
1,77070100100700ff@1,Leistung,W,Power_curr,0
1,77070100010800ff@1000,Verbrauch,KWh,Total_in,2
1,77070100020800ff@1000,Netzeinspeisung,KWh,Total_out,2
#
